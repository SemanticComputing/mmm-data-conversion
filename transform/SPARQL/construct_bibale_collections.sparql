PREFIX type: <http://info.deepcarbon.net/schema/type#>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX mmm-actors: <http://ldf.fi/mmm/actor/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT {
  ?collection_mmm a crm:E78_Collection ;
    skos:prefLabel ?label ;
    skos:altLabel ?altlabel ;
    crm:P92i_was_brought_into_existence_by ?creation_event ;
    mmm-schema:collection_type ?collection_type_literal ;
    mmm-schema:collection_location ?collection_place_mmm ;
    .

  ?creation_event a crm:E63_Beginning_of_Existence ;
    crm:P4_has_time-span ?creation_date_mmm ;
    .

  ?creation_date_mmm a crm:E52_Time-Span ;
    skos:prefLabel ?creation_date_label ;
    crm:P82a_begin_of_the_begin ?creation_date_bb ;
    crm:P81a_end_of_the_begin ?creation_date_eb ;
    crm:P81b_begin_of_the_end ?creation_date_be ;
    crm:P82b_end_of_the_end ?creation_date_ee ;
    .

  ?dispersion_event a crm:E64_End_of_Existence ;
    crm:P4_has_time-span ?dispersion_date_mmm ;
    .

  ?dispersion_date_mmm a crm:E52_Time-Span ;
    skos:prefLabel ?dispersion_date_label ;
    crm:P82a_begin_of_the_begin ?dispersion_date_bb ;
    crm:P81a_end_of_the_begin ?dispersion_date_eb ;
    crm:P81b_begin_of_the_end ?dispersion_date_be ;
    crm:P82b_end_of_the_end ?dispersion_date_ee ;
    .

}
WHERE
{
  ?collection a crm:E78_Collection ;
              rdfs:label ?label .

  OPTIONAL { ?collection bibale:type:collection_type ?collection_type_literal }
  OPTIONAL { ?collection bibale:type:collection_place ?collection_place }

  OPTIONAL {  # This exists for 23 collections
    ?collection crm:P1_is_identified_by/rdfs:label ?altlabel_ .
  	filter (?label != ?altlabel_)
    BIND(?altlabel_ as ?altlabel)
  }

  # COLLECTION CREATION DATE
  OPTIONAL {
  	?collection bibale:type:creation_date/crm:P4_has_time-span ?creation_date .
    OPTIONAL { ?creation_date rdfs:label ?creation_date_label . }
    OPTIONAL { ?creation_date crm:P82a_begin_of_the_begin ?creation_date_bb }
    OPTIONAL { ?creation_date crm:P81a_end_of_the_begin ?creation_date_eb }
    OPTIONAL { ?creation_date crm:P81b_begin_of_the_end ?creation_date_be }
    OPTIONAL { ?creation_date crm:P82b_end_of_the_end ?creation_date_ee }
  }

  # COLLECTION DISPERSION DATE
  OPTIONAL {
  	?collection bibale:type:dispersion_date/crm:P4_has_time-span ?dispersion_date .
    OPTIONAL { ?dispersion_date rdfs:label ?dispersion_date_label . }
    OPTIONAL { ?dispersion_date crm:P82a_begin_of_the_begin ?dispersion_date_bb }
    OPTIONAL { ?dispersion_date crm:P81a_end_of_the_begin ?dispersion_date_eb }
    OPTIONAL { ?dispersion_date crm:P81b_begin_of_the_end ?dispersion_date_be }
    OPTIONAL { ?dispersion_date crm:P82b_end_of_the_end ?dispersion_date_ee }
  }

  BIND(REPLACE(STR(?collection), "^.*\\/(.+)", "$1") as ?collection_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/collection/bibale_", ?collection_local_id)) as ?collection_mmm)

  BIND(IRI(CONCAT("http://ldf.fi/mmm/collection/bibale_", ?collection_local_id, "_creation")) as ?creation_event)

  BIND(REPLACE(STR(?collection_place), "^.*\\/(.+)", "$1") as ?collection_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?collection_place_local_id, ":", "_"))) as ?collection_place_mmm)

  BIND(REPLACE(STR(?creation_date), "^.*\\/(.+)", "$1") as ?creation_date_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?creation_date_local_id, ":", "_"))) as ?creation_date_mmm)

  BIND(REPLACE(STR(?dispersion_date), "^.*\\/(.+)", "$1") as ?dispersion_date_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?dispersion_date_local_id, ":", "_"))) as ?dispersion_date_mmm)
}
