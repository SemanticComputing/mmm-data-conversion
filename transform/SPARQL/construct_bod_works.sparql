PREFIX frbr: <http://www.cidoc-crm.org/frbr/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/schema/mmm/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bod-roles: <https://medieval.bodleian.ox.ac.uk/catalog/authority/roles/>

CONSTRUCT {
  ?work_mmm a frbroo:F1_Work  ;
    skos:prefLabel ?work_preflabel ;
    mmms:data_provider_url ?work ;
    dct:source mmms:Bodley ;
    .

  ?work_conception_mmm a frbroo:F27_Work_Conception ;
    skos:prefLabel ?work_conception_pref_label ;
    frbroo:R16_initiated ?work_mmm ;
    mmms:carried_out_by_as_author ?work_conception_author_mmm ;  # TODO: P14 carried out by
    dct:source mmms:Bodley ;
    .
}
WHERE
{
  ?work a frbr:F1_Work ;
        rdfs:label ?work_title .
  OPTIONAL { ?work crm:P102_has_title/rdfs:label ?work_preflabel . }
  OPTIONAL {
    ?work frbr:R16i_was_initiated_by ?work_conception .
    ?work_conception rdfs:label ?work_conception_pref_label .
    ?work_conception crm:P14_carried_out_by ?work_conception_author .
  }
  BIND(REPLACE(STR(?work), "^.*\\/(.+)", "$1") as ?work_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/work/bodley_", ?work_local_id)) as ?work_mmm)

  BIND(REPLACE(REPLACE(STR(?work_conception), "^.+catalog\\/(.+)", "$1"), "\\/|:|#|%", "-") as ?work_conception_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/event/bodley_work_conception", ?work_conception_local_id)) as ?work_conception_mmm)

  BIND(REPLACE(STR(?work_conception_author), "^.*\\/(.+)", "$1") as ?actor_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?actor_local_id)) as ?work_conception_author_mmm)
}
