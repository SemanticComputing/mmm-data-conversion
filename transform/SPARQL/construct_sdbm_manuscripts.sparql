PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>

# TODO: Folio count URIs etc. to be based on the count to avoid duplicates
# TODO: Material URIs etc. to be based on the material to avoid duplicates

# TODO: Get last known location from Collection or from last provenance event

CONSTRUCT {

  ?manifestation_singleton a frbroo:F4_Manifestation_Singleton ;
    skos:prefLabel ?manuscript_preflabel ;
    ecrm:P128_carries ?expression ;
    ecrm:P43_has_dimension ?width_uri ;
    ecrm:P43_has_dimension ?height_uri ;
    ecrm:P43_has_dimension ?folios_uri ;
    ecrm:P43_has_dimension ?lines_uri ;
    ecrm:P43_has_dimension ?columns_uri ;
    ecrm:P43_has_dimension ?miniatures_uri ;
    ecrm:P45_consists_of ?material ;
    ecrm:P51_has_former_or_current_owner ?prov_agent ;
    mmm-schema:entry ?entry ;
    mmm-schema:manuscript_record ?manuscript ;
    mmm-schema:data_provider_url ?provider_url ; # for showing the link in faceted search
    mmm-schema:manuscript_author ?author ;  # shortcut
    mmm-schema:manuscript_work ?work ;  # shortcut
    # mmm-schema:language ?language ; # shortcut
    ?property_to_collection ?source_mmm ;
    ecrm:P70i_is_documented_in ?source_mmm ;
    mmm-schema:catalog_or_lot_number ?cat_lot_number ;
    ecrm:P3_has_note ?other_info ;
    mmm-schema:phillipps_number ?phillipps ;
    mmm-schema:shelfmark_buchanan ?shelfmark_buchanan ;
    mmm-schema:shelfmark_bnf_latin ?shelfmark_bnf_latin ;
    mmm-schema:shelfmark_bnf_hebreu ?shelfmark_bnf_hebreu ;
    mmm-schema:shelfmark_bnf_nal ?shelfmark_bnf_nal ;
    mmm-schema:shelfmark_arsenal ?shelfmark_arsenal ;
    mmm-schema:shelfmark_christ_church ?shelfmark_christ_church ;
    mmm-schema:shelfmark_barocci ?shelfmark_barocci ;
    dct:source mmm-schema:SDBM .

  ?parent_manifestation_singleton
    a frbroo:F4_Manifestation_Singleton ;
    skos:prefLabel ?parent_preflabel ;
    ecrm:P46_is_composed_of ?manifestation_singleton ;
    mmm-schema:data_provider_url ?provider_url ; # for showing the link in faceted search
    dct:source mmm-schema:SDBM .

  ?production a ecrm:E12_Production ;
    # skos:prefLabel # TODO
    ecrm:P108_has_produced ?manifestation_singleton ;
    ecrm:P4_has_time-span ?production_timespan ;
    ecrm:P7_took_place_at ?production_place ;
    # mmm-schema:carried_out_by_as_artist ?production_artist ;
    # mmm-schema:carried_out_by_as_scribe ?production_scribe ;
    # dct:source mmm-schema:SDBM .
    .

  ?production_timespan a ecrm:E52_Time-Span ;
    skos:prefLabel ?production_date_label ;
    skos:altLabel ?production_date_observed ;
    ecrm:P82a_begin_of_the_begin ?production_date_start ;
    ecrm:P82b_end_of_the_end ?production_date_end .

  ?width_uri a ?width_class ;
    ecrm:P90_has_value ?width_value ;
    ecrm:P91_has_unit "mm"@en .

  ?height_uri a ?height_class ;
    ecrm:P90_has_value ?height_value ;
    ecrm:P91_has_unit "mm"@en .

  ?folios_uri a ?folios_class ;
    ecrm:P90_has_value ?folios_value .

  ?lines_uri a ?lines_class ;
    ecrm:P90_has_value ?lines_value .

  ?columns_uri a ?columns_class ;
    ecrm:P90_has_value ?columns_value .

  ?miniatures_uri a ?miniatures_class ;
    ecrm:P90_has_value ?miniatures_value .

  # provenance reification
  # ?reification_owner_uri
  #   a rdf:Statement ;
  #   rdf:subject ?manifestation_singleton ;
  #   rdf:predicate ecrm:P51_has_former_or_current_owner ;
  #   rdf:object ?prov_agent ;
  #   mmm-schema:order ?prov_order ;
  #   rdfs:comment ?prov_comment ;
  #   mmm-schema:data_provider_url ?entry ;
  #   # dct:source ?source .
  #   .

  ?observation_mmm  # TODO: prefLabel
    a ecrm:E7_Activity ;
    mmm-schema:observed_manuscript ?manifestation_singleton ;
    mmm-schema:ownership_attributed_to ?prov_agent ;
    mmm-schema:order ?prov_order ;
    ecrm:P3_has_note ?prov_comment ;
    mmm-schema:data_provider_url ?entry ;
    dct:source ?source ;
    .

}
# SELECT DISTINCT ?entry ?entry_manuscript ?entry_source ?title ?common_title ?cat_lot_property ?other_info ?cat_lot_number ?phillipps ?shelfmark_bnf_latin ?shelfmark_bnf_hebreu ?shelfmark_bnf_nal ?shelfmark_buchanan ?shelfmark_arsenal ?shelfmark_christ_church ?shelfmark_barocci
WHERE {

  ?entry a :entries .
  FILTER(NOT EXISTS { ?entry :entries_deprecated true })

  OPTIONAL {
    ?entry_manuscript :entry_manuscripts_entry_id ?entry .
    ?entry_manuscript a :entry_manuscripts .
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "is" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript .
      ?manuscript a :manuscripts .
      ?manuscript :manuscripts_id ?manuscript_id_int .
    }
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "partial" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript_parent .
      ?entry_manuscript :entry_manuscripts_entry_id ?entry_partial_uri .
      ?manuscript_parent a :manuscripts .
      ?manuscript_parent :manuscripts_id ?manuscript_parent_id_int .
    }
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "possible" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript .
      ?manuscript a :manuscripts .
      ?manuscript :manuscripts_id ?manuscript_id_int .
      BIND("Possible match of manuscript"@en as ?entry_comment)
      # TODO: Adjust model to fit ?entry_comment (add observation event or add to transaction information)
    }
  }
  # manuscripts_location is a deprecated field in SDBM
  # OPTIONAL { ?manuscript :manuscripts_location ?location . }

  ## MANUSCRIPT DETAILS ##

  # TODO: Add dates

  OPTIONAL {
    ?provenance_entry :provenance_entry_id ?entry ;
                      :provenance_provenance_agent_id/:names_id ?prov_agent_id ;
                      :provenance_order ?prov_order ;
                      :provenance_id ?prov_id ;
    FILTER(STR(?prov_order) != "")
    OPTIONAL { ?provenance_entry :provenance_comment ?prov_comment . }
  }

  # title
  OPTIONAL {
    ?entry_title :entry_titles_entry_id ?entry .
    ?entry_title :entry_titles_id ?title_id .
    OPTIONAL {
      ?entry_title :entry_titles_title|:entry_titles_common_title ?title .
      FILTER(STR(?title) != "")
    }
    OPTIONAL {
      ?entry_title :entry_titles_common_title ?common_title .
      FILTER(STR(?common_title) != "")
    }
  }

  # author
  OPTIONAL {
    ?entry_author :entry_authors_entry_id ?entry .
    ?entry_author :entry_authors_author_id/:names_id ?author_id .
    ?entry_author :entry_authors_observed_name ?observed_author .
  }

  # production date
  OPTIONAL {
    ?entry_date :entry_dates_entry_id ?entry .
    ?entry_date :entry_dates_id ?production_date_id .
    ?entry_date :entry_dates_date_normalized_start ?production_date_start .
    ?entry_date :entry_dates_date_normalized_end ?production_date_end .
    ?entry_date :entry_dates_observed_date ?production_date_observed .
  }

  # artist

  # scribe

  # # language
  # OPTIONAL {
  #   ?entry_language :entry_languages_entry_id ?entry .
  #   ?entry_language :entry_languages_language_id/:languages_id ?language_id .
  #   ?entry_language :entry_languages_language_id/:languages_name ?language_ .
  #   BIND(STRLANG(?language_, 'en') AS ?language)
  # }

  # place
  OPTIONAL {
    ?entry_place :entry_places_entry_id ?entry .
    ?entry_place :entry_places_place_id/:places_id ?production_place_id .
  }

  # material
  OPTIONAL {
    ?entry_material :entry_materials_entry_id ?entry .
    ?entry_material :entry_materials_id ?material_id .
    ?entry_material :entry_materials_material ?material_ .
    BIND(STRLANG(?material_, 'en') AS ?material)
  }

  # use

  # folios
  OPTIONAL {
    ?entry :entries_folios ?folios_value .
    FILTER(STR(?folios_value) != "")
    BIND(mmm-schema:Folios as ?folios_class)
  }

  # lines
  OPTIONAL {
    ?entry :entries_num_lines ?lines_value .
    FILTER(STR(?lines_value) != "")
    BIND(mmm-schema:Lines as ?lines_class)
  }

  # columns
  OPTIONAL {
    ?entry :entries_num_columns ?columns_value .
    FILTER(STR(?columns_value) != "")
    BIND(mmm-schema:Columns as ?columns_class)
  }

  # width
  OPTIONAL {
    ?entry :entries_width ?width_value .
    FILTER(STR(?width_value) != "")
    BIND(mmm-schema:Width as ?width_class)
  }

  # height
  OPTIONAL {
    ?entry :entries_height ?height_value .
    FILTER(STR(?height_value) != "")
    BIND(mmm-schema:Height as ?height_class)
  }

  # alternate size

  # full-page miniatures
  OPTIONAL {
    OPTIONAL {
      ?entry :entries_miniatures_fullpage ?miniatures_fullpage_value .
      FILTER(STR(?miniatures_fullpage_value) != "")
    }

    # large miniatures
    OPTIONAL {
      ?entry :entries_miniatures_large ?miniatures_large_value .
      FILTER(STR(?miniatures_large_value) != "")
    }

    # small miniatures
    OPTIONAL {
      ?entry :entries_miniatures_small ?miniatures_small_value .
      FILTER(STR(?miniatures_small_value) != "")
    }

    # unspec. sized miniatures
    OPTIONAL {
      ?entry :entries_miniatures_unspec_size ?miniatures_unspec_value .
      FILTER(STR(?miniatures_unspec_value) != "")
    }
    BIND(?miniatures_large_value + ?miniatures_small_value + ?miniatures_fullpage_value + ?miniatures_unspec_value as ?miniatures_value)
    FILTER(?miniatures_value > 0)
    BIND(mmm-schema:Miniatures as ?miniatures_class)
  }

  # historiated initials

  # decorated initials

  # binding

  # manuscript link

  # other Info

  ## /MANUSCRIPT DETAILS ##

  ## SOURCE ##
  OPTIONAL {
    ?entry :entries_source_id ?entry_source .
    ?entry_source :sources_id ?source_id .

    OPTIONAL {
      # Collection catalogs
      ?entry_source :sources_source_type_id <https://sdbm.library.upenn.edu/source_types/2> .
      BIND(CONCAT(STR(mmm:), "collection/sdbm_") as ?source_namespace)
      BIND(ecrm:P46i_forms_part_of as ?property_to_collection)
      # TODO: Get last known location from collection
    }
    OPTIONAL {
      BIND(CONCAT(STR(mmm:), "source/sdbm_") as ?source_namespace)
    }

    OPTIONAL {
      # Get Phillipps number from cat_lot_number if source https://sdbm.library.upenn.edu/sources/12495
      FILTER (?entry_source = <https://sdbm.library.upenn.edu/sources/12495>)
      ?entry :entries_catalog_or_lot_number ?cat_lot_number .
      BIND(?cat_lot_number as ?phillipps)
    }
    OPTIONAL {
      # Get Bibliotheque de l'Arsenal shelfmark from cat_lot_number if source https://sdbm.library.upenn.edu/sources/33377
      FILTER (?entry_source = <https://sdbm.library.upenn.edu/sources/33377>)
      ?entry :entries_catalog_or_lot_number ?cat_lot_number .
      BIND(REPLACE(?cat_lot_number, "^Ms\\-(.+)\\.$", "$1") as ?shelfmark_arsenal)
      FILTER(?shelfmark_arsenal != ?cat_lot_number)
    }
    OPTIONAL {
      # Get Christ Church collection shelfmark from cat_lot_number if source https://sdbm.library.upenn.edu/sources/33463
      FILTER (?entry_source = <https://sdbm.library.upenn.edu/sources/33463>)
      ?entry :entries_catalog_or_lot_number ?cat_lot_number .
      BIND(?cat_lot_number as ?shelfmark_christ_church)
    }
    OPTIONAL {
      # Get Barocci collection shelfmark from cat_lot_number if source https://sdbm.library.upenn.edu/sources/132
      FILTER (?entry_source = <https://sdbm.library.upenn.edu/sources/132>)
      ?entry :entries_catalog_or_lot_number ?cat_lot_number .
      BIND(?cat_lot_number as ?shelfmark_barocci)
    }

    # # TODO: if source has no title, generate one for acquisition/observation prefLabel
    # OPTIONAL{
    #   ?entry_source :sources_title ?cat_title
    #   FILTER(STR(?cat_title) != "")
    # }
    # OPTIONAL{
    #   ?entry_source :sources_date ?cat_date_raw
    #   FILTER(STR(?cat_date_raw) != "")
    # }
    #OPTIONAL{ ?entry_source :sources_link ?cat_link }
    # OPTIONAL{
    #   ?source_agent :source_agents_source_id ?entry_source .
    #   ?source_agent :source_agents_agent_id ?source_agent_name .
    #   ?source_agent_name (^:name_places_name_id)/:name_places_place_id/:places_id ?source_place_local_id .
    #   BIND(IRI(CONCAT(STR(mmm:), "place/", STR(?source_place_local_id))) as ?source_agent_location)
    # }
  }

  OPTIONAL {
    ?entry :entries_catalog_or_lot_number ?cat_lot_number .
    FILTER(STR(?cat_lot_number) != "")
    OPTIONAL {
      # Get BNF Latin number
      ?entry :entries_catalog_or_lot_number ?cat_lot_number
      FILTER CONTAINS(?cat_lot_number, "Latin")
      BIND(REPLACE(?cat_lot_number, "^(.*)Latin *(\\d.*)$", "$2") as ?shelfmark_bnf_latin)
      FILTER(?shelfmark_bnf_latin != ?cat_lot_number)
    }
    OPTIONAL {
      # Get BNF Hébreu number
      ?entry :entries_catalog_or_lot_number ?cat_lot_number
      FILTER CONTAINS(?cat_lot_number, "Hébreu")
      BIND(REPLACE(?cat_lot_number, "^Hébreu *(\\d.*)$", "$1") as ?shelfmark_bnf_hebreu)
      FILTER(?shelfmark_bnf_hebreu != ?cat_lot_number)
    }
    OPTIONAL {
      # Get BNF NAL number
      ?entry :entries_catalog_or_lot_number ?cat_lot_number
      FILTER CONTAINS(?cat_lot_number, "NAL")
      BIND(REPLACE(?cat_lot_number, "^NAL *(\\d.*)$", "$1") as ?shelfmark_bnf_nal)
      FILTER(?shelfmark_bnf_nal != ?cat_lot_number)
    }
    OPTIONAL {
      # Get Buchanan number
      ?entry :entries_catalog_or_lot_number ?cat_lot_number
      FILTER CONTAINS(?cat_lot_number, "MS Buchanan")
      BIND(REPLACE(?cat_lot_number, "^MS Buchanan *(\\w.*)$", "$1") as ?shelfmark_buchanan)
      FILTER(?shelfmark_buchanan != ?cat_lot_number)
    }
  }

  OPTIONAL {
    ?entry :entries_other_info ?other_info
    FILTER(STR(?other_info) != "")
    OPTIONAL {
      # Get Phillipps number
      ?entry :entries_other_info ?other_info
      FILTER CONTAINS(?other_info, "Phillipps")
      BIND(REPLACE(?other_info, "^(.*)Phillipps *(\\d+)(.*)$", "$2") as ?phillipps)
      FILTER(?phillipps != ?other_info)
    }
  }

  BIND(SUBSTR(?cat_date_raw, 1, 4) as ?cat_date_year)
  BIND(SUBSTR(?cat_date_raw, 5, 2) as ?cat_date_month)
  BIND(SUBSTR(?cat_date_raw, 7) as ?cat_date_day)
  BIND(?cat_date_year + "-" + ?cat_date_month + "-" + ?cat_date_day as ?cat_date)

  BIND(REPLACE(STR(?entry), "^.*\\/(.+)", "$1") as ?entry_id)
  BIND(REPLACE(STR(?entry_partial_uri), "^.*\\/(.+)", "$1") as ?entry_partial_id)

  # generate IRIs in LDF.fi namespace
  BIND(STR(?manuscript_id_int) AS ?manuscript_id_)
  BIND(COALESCE(?manuscript_id_, "part_" + STR(?entry_partial_id), "orphan_" + STR(?entry_id)) AS ?manuscript_id)
  BIND(IRI(CONCAT(STR(mmm:), "work/", STR(?title_id))) as ?work)
  BIND(IRI(CONCAT(STR(mmm:), "work_conception/", STR(?title_id))) as ?work_conception)
  BIND(IRI(CONCAT(STR(mmm:), "expression/", STR(?title_id))) as ?expression)
  BIND(IRI(CONCAT(STR(mmm:), "expression_creation/", STR(?title_id))) as ?expression_creation)
  BIND(IRI(CONCAT(STR(mmm:), "manifestation_singleton/sdbm_", ?manuscript_id)) as ?manifestation_singleton)
  BIND(IRI(CONCAT(STR(mmm:), "manifestation_singleton/sdbm_", STR(?manuscript_parent_id_int))) as ?parent_manifestation_singleton)
  BIND(IRI(CONCAT(STR(mmm:), "production/", ?manuscript_id)) as ?production)
  BIND(IRI(CONCAT(?source_namespace, STR(?source_id))) as ?source_mmm)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", STR(?author_id))) as ?author)
  #BIND(IRI(CONCAT(STR(mmm:), "actor/", STR(?seller_id))) as ?seller)
  #BIND(IRI(CONCAT(STR(mmm:), "actor/", STR(?buyer_id))) as ?buyer)
  #BIND(IRI(CONCAT(STR(mmm:), "actor/", STR(?selling_agent_id))) as ?selling_agent)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", STR(?prov_agent_id))) as ?prov_agent)
  BIND(IRI(CONCAT(STR(mmm:), "place/sdbm_", STR(?production_place_id))) as ?production_place)
  BIND(IRI(CONCAT(STR(mmm:), "timespan/", STR(?production_date_id))) as ?production_timespan)
  #BIND(IRI(CONCAT(STR(mmm:), "observation/", ?entry_id)) as ?acquisition_uri)
  BIND(IRI(CONCAT(STR(mmm:), "height/", ?entry_id)) as ?height_uri)
  BIND(IRI(CONCAT(STR(mmm:), "width/", ?entry_id)) as ?width_uri)
  BIND(IRI(CONCAT(STR(mmm:), "folios/", ?entry_id)) as ?folios_uri)
  BIND(IRI(CONCAT(STR(mmm:), "lines/", ?entry_id)) as ?lines_uri)
  BIND(IRI(CONCAT(STR(mmm:), "columns/", ?entry_id)) as ?columns_uri)
  BIND(IRI(CONCAT(STR(mmm:), "miniatures/", ?entry_id)) as ?miniatures_uri)
  #BIND(IRI(CONCAT(STR(mmm:), "monetary_amounts/", ?sales_id)) as ?monetary_amount_uri)

  BIND(CONCAT(STR(?production_date_start),"-",STR(?production_date_end)) as ?production_date_label)
  BIND("SDBM_MS_" + STR(?manuscript_id) as ?manuscript_preflabel)
  BIND("SDBM_MS_" + STR(?manuscript_parent_id_int) as ?parent_preflabel)

  BIND(COALESCE(?manuscript, ?entry) as ?provider_url)

  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_author_", STR(?author_id))) as ?reification_author_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_owner_", STR(?prov_agent_id))) as ?reification_owner_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_production_place_", STR(?place_id))) as ?reification_production_place_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_production_time_", STR(?production_date_id))) as ?reification_production_time_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_identifier_", ?manuscript_id)) as ?reification_id_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_material_", ?material_id)) as ?reification_material_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_folios_", STR(?folios_value))) as ?reification_folios_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_lines_", STR(?lines_value))) as ?reification_lines_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_columns_", STR(?columns_value))) as ?reification_columns_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_height_", STR(?height_value))) as ?reification_height_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_width_", STR(?width_value))) as ?reification_width_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_miniatures_", STR(?miniatures_value))) as ?reification_miniatures_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_language_", REPLACE(?language, " ", "-"))) as ?reification_language_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_label_", STR(?title_id))) as ?reification_label_uri)
  # BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_label_", STR(?title_id), "_", SUBSTR(REPLACE(?title, "[\\\"\\~\\.\\-\\!\\$\\&\\'\\(\\)\\*\\+\\,\\;\\=\\/\\?\\#\\@\\%\\_\\{\\}\\[\\]\\^\\s\\`]", "-"), 0, 21))) as ?reification_label_uri)

  BIND(IRI(CONCAT(STR(mmm:), "observation/sdbm_", STR(?prov_id))) as ?observation_mmm)
}
