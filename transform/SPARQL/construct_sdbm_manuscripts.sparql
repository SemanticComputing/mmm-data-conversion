PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/schema/mmm/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# TODO: prefLabels for measurements

# TODO: Separate production events for each time-span (manuscript date in SDBM), see e.g. http://ldf.fi/mmm/manifestation_singleton/sdbm_orphan_254076

CONSTRUCT {

  ?manifestation_singleton a frbroo:F4_Manifestation_Singleton ;
    skos:prefLabel ?manuscript_preflabel ;
    ecrm:P128_carries ?expression ;
    mmms:width ?width_uri ;
    mmms:height ?height_uri ;
    mmms:folios ?folios_uri ;
    mmms:lines ?lines_uri ;
    mmms:columns ?columns_uri ;
    mmms:miniatures ?miniatures_uri ;
    mmms:decorated_initials ?decorated_initials_uri ;
    mmms:historiated_initials ?historiated_initials_uri ;
    ecrm:P45_consists_of ?material_uri_mmm ;
    ecrm:P51_has_former_or_current_owner ?prov_agent ;
    # mmms:entry ?entry ;
    # mmms:manuscript_record ?manuscript ;
    mmms:data_provider_url ?provider_url ; # for showing the link in faceted search
    mmms:manuscript_author ?author ;  # shortcut
    mmms:manuscript_work ?work ;  # shortcut
    # mmms:language ?language ; # shortcut
    ?property_to_collection ?source_mmm ;
    ecrm:P70i_is_documented_in ?source_mmm ;
    mmms:catalog_or_lot_number ?cat_lot_number ;
    ecrm:P3_has_note ?other_info ;
    mmms:phillipps_number ?phillipps ;
    mmms:shelfmark_buchanan ?shelfmark_buchanan ;
    mmms:shelfmark_bnf_latin ?shelfmark_bnf_latin ;
    mmms:shelfmark_bnf_hebreu ?shelfmark_bnf_hebreu ;
    mmms:shelfmark_bnf_nal ?shelfmark_bnf_nal ;
    mmms:shelfmark_arsenal ?shelfmark_arsenal ;
    mmms:shelfmark_christ_church ?shelfmark_christ_church ;
    mmms:shelfmark_barocci ?shelfmark_barocci ;
    mmms:binding ?binding_text ;
    mmms:external_url ?external_url ;
    dct:source mmms:SDBM .

  ?parent_manifestation_singleton
    a frbroo:F4_Manifestation_Singleton ;
    skos:prefLabel ?parent_preflabel ;
    ecrm:P46_is_composed_of ?manifestation_singleton ;
    mmms:data_provider_url ?provider_url ; # for showing the link in faceted search
    dct:source mmms:SDBM .

  ?production a ecrm:E12_Production ;
    skos:prefLabel ?production_label ;
    ecrm:P108_has_produced ?manifestation_singleton ;
    ecrm:P4_has_time-span ?production_timespan ;
    ecrm:P7_took_place_at ?production_place_mmm ;
    mmms:carried_out_by_as_artist ?artist ;
    mmms:carried_out_by_as_scribe ?scribe ;
    dct:source mmms:SDBM ;
    .

  ?production_timespan a ecrm:E52_Time-Span ;
    skos:prefLabel ?production_timespan_label ;
    skos:altLabel ?production_date_observed ;
    ecrm:P82a_begin_of_the_begin ?production_date_start_mmm ;
    ecrm:P82b_end_of_the_end ?production_date_end_mmm ;
    dct:source mmms:SDBM ;
    .

  ?width_uri a mmms:Width ;
    ecrm:P91_has_unit mmms:Millimetre ;
    ecrm:P90_has_value ?width_value .

  ?height_uri a mmms:Height ;
    ecrm:P91_has_unit mmms:Millimetre ;
    ecrm:P90_has_value ?height_value .

  ?folios_uri a mmms:Folios ;
    ecrm:P90_has_value ?folios_value .

  ?lines_uri a mmms:Lines ;
    ecrm:P90_has_value ?lines_value .

  ?columns_uri a mmms:Columns ;
    ecrm:P90_has_value ?columns_value .

  ?miniatures_uri a mmms:Miniatures ;
    ecrm:P90_has_value ?miniatures_value .

  ?decorated_initials_uri a mmms:DecoratedInitials ;
    ecrm:P90_has_value ?decorated_initials_value .

  ?historiated_initials_uri a mmms:HistoriatedInitials ;
    ecrm:P90_has_value ?historiated_initials_value .

  ?material_uri_mmm a ecrm:E57_Material ;
    skos:prefLabel ?material_label .

  # provenance reification
  # ?reification_owner_uri
  #   a rdf:Statement ;
  #   rdf:subject ?manifestation_singleton ;
  #   rdf:predicate ecrm:P51_has_former_or_current_owner ;
  #   rdf:object ?prov_agent ;
  #   mmms:order ?prov_order ;
  #   rdfs:comment ?prov_comment ;
  #   mmms:data_provider_url ?entry ;
  #   # dct:source ?source .
  #   .

  ?observation_mmm  # TODO: prefLabel
    a ecrm:E7_Activity ;
    mmms:observed_manuscript ?manifestation_singleton ;
    mmms:ownership_attributed_to ?prov_agent ;
    mmms:order ?prov_order ;
    ecrm:P3_has_note ?prov_comment ;
    mmms:data_provider_url ?entry ;
    dct:source ?source ;
    .

}
# SELECT DISTINCT ?entry ?entry_manuscript ?entry_source ?title ?common_title ?cat_lot_property ?other_info ?cat_lot_number ?phillipps ?shelfmark_bnf_latin ?shelfmark_bnf_hebreu ?shelfmark_bnf_nal ?shelfmark_buchanan ?shelfmark_arsenal ?shelfmark_christ_church ?shelfmark_barocci
WHERE {

  ?entry a :entries .
  FILTER(NOT EXISTS { ?entry :entries_deprecated true })

  OPTIONAL {
    ?entry_manuscript :entry_manuscripts_entry_id ?entry .
    ?entry_manuscript a :entry_manuscripts .
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "is" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript .
      ?manuscript a :manuscripts .
    }
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "partial" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript_parent .
      ?entry_manuscript :entry_manuscripts_entry_id ?entry_partial_uri .
      ?manuscript_parent a :manuscripts .
    }
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "possible" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript .
      ?manuscript a :manuscripts .
      BIND("Possible match of manuscript" as ?entry_comment)
      # TODO: Adjust model to fit ?entry_comment (add observation event or add to transaction information)
    }
  }
  # manuscripts_location is a deprecated field in SDBM
  # OPTIONAL { ?manuscript :manuscripts_location ?location . }

  ## MANUSCRIPT DETAILS ##

  OPTIONAL {
    ?provenance_entry :provenance_entry_id ?entry ;
                      :provenance_provenance_agent_id ?prov_agent_ ;
                      :provenance_order ?prov_order .
    FILTER NOT EXISTS { ?prov_agent_ :names_deleted true }
    FILTER(STR(?prov_order) != "")
    OPTIONAL { ?provenance_entry :provenance_comment ?prov_comment . }
  }

  # title
  OPTIONAL {
    ?entry_title :entry_titles_entry_id ?entry .
    OPTIONAL {
      ?entry_title :entry_titles_title|:entry_titles_common_title ?title .
      FILTER(STR(?title) != "")
    }
    OPTIONAL {
      ?entry_title :entry_titles_common_title ?common_title .
      FILTER(STR(?common_title) != "")
    }
  }

  # author
  OPTIONAL {
    ?entry_author :entry_authors_entry_id ?entry .
    ?entry_author :entry_authors_author_id ?author_ .
    FILTER NOT EXISTS { ?author_ :names_deleted true }
    # ?entry_author :entry_authors_observed_name ?observed_author .
  }

  # production date
  OPTIONAL {
    ?entry_date :entry_dates_entry_id ?entry .
    ?entry_date :entry_dates_date_normalized_start ?production_year_start .
    ?entry_date :entry_dates_date_normalized_end ?production_year_end .
    ?entry_date :entry_dates_observed_date ?production_date_observed .

    OPTIONAL {
      ?entry_date :entry_dates_date_normalized_start ?production_year_start .
      FILTER(?production_year_start != "")
      BIND(IF(STRSTARTS(?production_year_start, "-"),
          CONCAT("-", SUBSTR("0000", STRLEN(?production_year_start)), SUBSTR(?production_year_start, 2, 4)),
          CONCAT(SUBSTR("0000", STRLEN(?production_year_start) + 1), ?production_year_start)) as ?production_year_start_iso)
    }

    OPTIONAL {
      ?entry_date :entry_dates_date_normalized_end ?production_year_end .
      FILTER(?production_year_end != "")
      BIND(IF(STRSTARTS(?production_year_end, "-"),
          CONCAT("-", SUBSTR("0000", STRLEN(?production_year_end)), SUBSTR(?production_year_end, 2, 4)),
          CONCAT(SUBSTR("0000", STRLEN(?production_year_end) + 1), ?production_year_end)) as ?production_year_end_iso)
    }

    BIND(STRDT(?production_year_start_iso + "-01-01", xsd:date) as ?production_date_start_mmm)
    BIND(STRDT(?production_year_end_iso + "-12-31", xsd:date) as ?production_date_end_mmm)
  }

  # TODO: artist
  OPTIONAL {
    ?entry_artists :entry_artists_entry_id ?entry .
    ?entry_artists :entry_artists_artist_id ?artist_ .
    FILTER NOT EXISTS { ?artist_ :names_deleted true }
  }

  # TODO: scribe
  OPTIONAL {
    ?entry_scribes :entry_scribes_entry_id ?entry .
    ?entry_scribes :entry_scribes_scribe_id ?scribe_ .
    FILTER NOT EXISTS { ?scribe_ :names_deleted true }
  }

  # place
  OPTIONAL {
    ?entry_place :entry_places_entry_id ?entry .
    ?entry_place :entry_places_place_id ?production_place .
  }

  # material
  OPTIONAL {
    ?entry_material :entry_materials_entry_id ?entry .
    ?entry_material :entry_materials_material ?material_label .
  }

  # use

  # folios
  OPTIONAL {
    ?entry :entries_folios ?folios_value .
    FILTER(STR(?folios_value) != "")
    BIND(IRI(CONCAT(STR(mmm:), "folios/", STR(?folios_value))) as ?folios_uri)
  }

  # lines
  OPTIONAL {
    ?entry :entries_num_lines ?lines_value .
    FILTER(STR(?lines_value) != "")
    BIND(IRI(CONCAT(STR(mmm:), "lines/", STR(?lines_value))) as ?lines_uri)
  }

  # columns
  OPTIONAL {
    ?entry :entries_num_columns ?columns_value .
    FILTER(STR(?columns_value) != "")
    BIND(IRI(CONCAT(STR(mmm:), "columns/", STR(?columns_value))) as ?columns_uri)
  }

  # width
  OPTIONAL {
    ?entry :entries_width ?width_value .
    FILTER(STR(?width_value) != "")
    BIND(IRI(CONCAT(STR(mmm:), "width/", STR(?width_value))) as ?width_uri)
  }

  # height
  OPTIONAL {
    ?entry :entries_height ?height_value .
    FILTER(STR(?height_value) != "")
    BIND(IRI(CONCAT(STR(mmm:), "height/", STR(?height_value))) as ?height_uri)
  }

  # alternate size

  # miniatures
  OPTIONAL {
    # full-page miniatures
    OPTIONAL {
      ?entry :entries_miniatures_fullpage ?miniatures_fullpage_value .
      FILTER(STR(?miniatures_fullpage_value) != "")
    }

    # large miniatures
    OPTIONAL {
      ?entry :entries_miniatures_large ?miniatures_large_value .
      FILTER(STR(?miniatures_large_value) != "")
    }

    # small miniatures
    OPTIONAL {
      ?entry :entries_miniatures_small ?miniatures_small_value .
      FILTER(STR(?miniatures_small_value) != "")
    }

    # unspec. sized miniatures
    OPTIONAL {
      ?entry :entries_miniatures_unspec_size ?miniatures_unspec_value .
      FILTER(STR(?miniatures_unspec_value) != "")
    }
    BIND(?miniatures_large_value + ?miniatures_small_value + ?miniatures_fullpage_value + ?miniatures_unspec_value as ?miniatures_value)
    FILTER(?miniatures_value > 0)
    BIND(IRI(CONCAT(STR(mmm:), "miniatures/", STR(?miniatures_value))) as ?miniatures_uri)
  }

  # historiated initials
  OPTIONAL {
    ?entry :entries_initials_historiated ?historiated_initials_value .
    FILTER(STR(?historiated_initials_value) != "")
    BIND(IRI(CONCAT(STR(mmm:), "historiated_initials/", STR(?historiated_initials_value))) as ?historiated_initials_uri)
  }

  # decorated initials
  OPTIONAL {
    ?entry :entries_initials_decorated ?decorated_initials_value .
    FILTER(STR(?decorated_initials_value) != "")
    BIND(IRI(CONCAT(STR(mmm:), "decorated_initials/", STR(?decorated_initials_value))) as ?decorated_initials_uri)
  }
  # binding
  OPTIONAL {
    ?entry :entries_manuscript_binding ?binding_text .
    FILTER(STR(?binding_text) != "")
  }

  # manuscript link
  OPTIONAL {
    ?entry :entries_manuscript_link ?external_url_ .
    BIND(URI(?external_url) as ?external_url)
  }

  ## /MANUSCRIPT DETAILS ##

  ## SOURCE ##
  OPTIONAL {
    ?entry :entries_source_id ?entry_source .

    FILTER(NOT EXISTS { ?entry_source :sources_deleted true })

    OPTIONAL {
      # Collection catalogs
      ?entry_source :sources_source_type_id <https://sdbm.library.upenn.edu/source_types/2> .
      BIND(CONCAT(STR(mmm:), "collection/sdbm_") as ?source_namespace)
      BIND(ecrm:P46i_forms_part_of as ?property_to_collection)
    }
    OPTIONAL {
      BIND(CONCAT(STR(mmm:), "source/sdbm_") as ?source_namespace)
    }

    OPTIONAL {
      # Get Phillipps number from cat_lot_number if source https://sdbm.library.upenn.edu/sources/12495
      FILTER (?entry_source = <https://sdbm.library.upenn.edu/sources/12495>)
      ?entry :entries_catalog_or_lot_number ?cat_lot_number .
      BIND(?cat_lot_number as ?phillipps)
    }
    OPTIONAL {
      # Get Bibliotheque de l'Arsenal shelfmark from cat_lot_number if source https://sdbm.library.upenn.edu/sources/33377
      FILTER (?entry_source = <https://sdbm.library.upenn.edu/sources/33377>)
      ?entry :entries_catalog_or_lot_number ?cat_lot_number .
      BIND(REPLACE(?cat_lot_number, "^Ms\\-(.+)\\.$", "$1") as ?shelfmark_arsenal)
      FILTER(?shelfmark_arsenal != ?cat_lot_number)
    }
    OPTIONAL {
      # Get Christ Church collection shelfmark from cat_lot_number if source https://sdbm.library.upenn.edu/sources/33463
      FILTER (?entry_source = <https://sdbm.library.upenn.edu/sources/33463>)
      ?entry :entries_catalog_or_lot_number ?cat_lot_number .
      BIND(?cat_lot_number as ?shelfmark_christ_church)
    }
    OPTIONAL {
      # Get Barocci collection shelfmark from cat_lot_number if source https://sdbm.library.upenn.edu/sources/132
      FILTER (?entry_source = <https://sdbm.library.upenn.edu/sources/132>)
      ?entry :entries_catalog_or_lot_number ?cat_lot_number .
      BIND(?cat_lot_number as ?shelfmark_barocci)
    }
  }

  OPTIONAL {
    ?entry :entries_catalog_or_lot_number ?cat_lot_number .
    FILTER(STR(?cat_lot_number) != "")
    OPTIONAL {
      # Get BNF Latin number
      ?entry :entries_catalog_or_lot_number ?cat_lot_number
      FILTER CONTAINS(?cat_lot_number, "Latin")
      BIND(REPLACE(?cat_lot_number, "^(.*)Latin *(\\d.*)$", "$2") as ?shelfmark_bnf_latin)
      FILTER(?shelfmark_bnf_latin != ?cat_lot_number)
    }
    OPTIONAL {
      # Get BNF Hébreu number
      ?entry :entries_catalog_or_lot_number ?cat_lot_number
      FILTER CONTAINS(?cat_lot_number, "Hébreu")
      BIND(REPLACE(?cat_lot_number, "^Hébreu *(\\d.*)$", "$1") as ?shelfmark_bnf_hebreu)
      FILTER(?shelfmark_bnf_hebreu != ?cat_lot_number)
    }
    OPTIONAL {
      # Get BNF NAL number
      ?entry :entries_catalog_or_lot_number ?cat_lot_number
      FILTER CONTAINS(?cat_lot_number, "NAL")
      BIND(REPLACE(?cat_lot_number, "^NAL *(\\d.*)$", "$1") as ?shelfmark_bnf_nal)
      FILTER(?shelfmark_bnf_nal != ?cat_lot_number)
    }
    OPTIONAL {
      # Get Buchanan number
      ?entry :entries_catalog_or_lot_number ?cat_lot_number
      FILTER CONTAINS(?cat_lot_number, "MS Buchanan")
      BIND(REPLACE(?cat_lot_number, "^MS Buchanan *(\\w.*)$", "$1") as ?shelfmark_buchanan)
      FILTER(?shelfmark_buchanan != ?cat_lot_number)
    }
  }

  OPTIONAL {
    ?entry :entries_other_info ?other_info
    FILTER(STR(?other_info) != "")
    OPTIONAL {
      # Get Phillipps number
      ?entry :entries_other_info ?other_info
      FILTER CONTAINS(?other_info, "Phillipps")
      BIND(REPLACE(?other_info, "^(.*)Phillipps *0*([1-9]\\d+)(.*)$", "$2") as ?phillipps)
      FILTER(?phillipps != ?other_info)
    }
  }

  BIND(REPLACE(STR(?entry), "^.*\\/(.+)", "$1") as ?entry_id)
  BIND(REPLACE(STR(?entry_partial_uri), "^.*\\/(.+)", "$1") as ?entry_partial_id)

  # generate IRIs in LDF.fi namespace
  BIND(REPLACE(STR(?manuscript), "^.*\\/(.+)", "$1") as ?manuscript_id_)
  BIND(COALESCE(?manuscript_id_, "part_" + STR(?entry_partial_id), "orphan_" + STR(?entry_id)) AS ?manuscript_id)

  BIND(REPLACE(STR(?entry_title), "^.*\\/(.+)", "$1") as ?title_id)
  BIND(IRI(CONCAT(STR(mmm:), "work/", ?title_id)) as ?work)
  BIND(IRI(CONCAT(STR(mmm:), "expression/", ?title_id)) as ?expression)
  BIND(IRI(CONCAT(STR(mmm:), "event/sdbm_expression_creation_", ?title_id)) as ?expression_creation)

  BIND(IRI(CONCAT(STR(mmm:), "manifestation_singleton/sdbm_", ?manuscript_id)) as ?manifestation_singleton)

  BIND(REPLACE(STR(?manuscript_parent), "^.*\\/(.+)", "$1") as ?manuscript_parent_id)
  BIND(IRI(CONCAT(STR(mmm:), "manifestation_singleton/sdbm_", ?manuscript_parent_id)) as ?parent_manifestation_singleton)
  BIND(IRI(CONCAT(STR(mmm:), "event/sdbm_production_", ?manuscript_id)) as ?production)

  BIND(REPLACE(STR(?entry_source), "^.*\\/(.+)", "$1") as ?source_id)
  BIND(IRI(CONCAT(?source_namespace, ?source_id)) as ?source_mmm)

  BIND(REPLACE(STR(?author_), "^.*\\/(.+)", "$1") as ?author_id)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", STR(?author_id))) as ?author)

  BIND(REPLACE(STR(?artist_), "^.*\\/(.+)", "$1") as ?artist_id)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", ?artist_id)) as ?artist)

  BIND(REPLACE(STR(?scribe_), "^.*\\/(.+)", "$1") as ?scribe_id)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", ?scribe_id)) as ?scribe)

  BIND(REPLACE(STR(?prov_agent_), "^.*\\/(.+)", "$1") as ?prov_agent_id)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", ?prov_agent_id)) as ?prov_agent)

  BIND(REPLACE(STR(?production_place), "^.*\\/(.+)", "$1") as ?production_place_id)
  BIND(IRI(CONCAT(STR(mmm:), "place/sdbm_", ?production_place_id)) as ?production_place_mmm)

  BIND(IRI(CONCAT(STR(mmm:), "time/production_", ?entry_id)) as ?production_timespan)

  BIND(CONCAT(STR(?production_year_start_iso)," - ",STR(?production_year_end_iso)) as ?production_timespan_label)
  BIND("SDBM_MS_" + ?manuscript_id as ?manuscript_preflabel)
  BIND("SDBM_MS_" + ?manuscript_parent_id as ?parent_preflabel)

  BIND(COALESCE(?manuscript, ?entry) as ?provider_url)

  BIND(IRI(CONCAT(STR(mmm:), "material/", LCASE(REPLACE(?material_label, "[ \\\"\\~\\.\\-\\!\\$\\&\\'\\(\\)\\*\\+\\,\\;\\=\\/\\?\\@\\%\\_\\{\\}\\[\\]\\^\\s\\`]", "-")))) as ?material_uri_mmm)

  BIND(REPLACE(STR(?provenance_entry), "^.*\\/(.+)", "$1") as ?provenance_entry_id)
  BIND(IRI(CONCAT(STR(mmm:), "event/sdbm_provenance", ?provenance_entry_id)) as ?observation_mmm)

  BIND(CONCAT("Production of ", ?manuscript_preflabel) as ?production_label)
}
