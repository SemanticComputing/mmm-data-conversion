PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbr: <http://www.cidoc-crm.org/frbr/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bod-roles: <https://medieval.bodleian.ox.ac.uk/catalog/authority/roles/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT {
  ?actor_mmm a ?actor_class ;
    skos:prefLabel ?preflabel ;
    skos:altLabel ?actor_label ;
    skos:altLabel ?altlabel ;
    ecrm:P98i_was_born ?birth_mmm ;
    ecrm:P100i_died_in ?death_mmm ;
    owl:sameAs ?external_uri ;
    mmm-schema:data_provider_url ?actor ;
    dct:source mmm-schema:Bodley ;
    .

  ?birth_mmm a ecrm:E67_Birth ;
    ecrm:P4_has_time-span ?birth_timespan_mmm ;
    skos:prefLabel ?birth_label ;
    .

  ?birth_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?birth_timespan_label ;
    ecrm:P82a_begin_of_the_begin ?birth_timespan_begin_begin ;
    ecrm:P82b_end_of_the_end ?birth_timespan_end_end ;
    .

  ?death_mmm a ecrm:E69_Death ;
    ecrm:P4_has_time-span ?death_timespan_mmm ;
    skos:prefLabel ?death_label ;
    .

  ?death_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?death_timespan_label ;
    ecrm:P82a_begin_of_the_begin ?death_timespan_begin_begin ;
    ecrm:P82b_end_of_the_end ?death_timespan_end_end ;
    .
}
WHERE
{
  {
    ?actor a crm:E21_Person .
    BIND(IRI(mmm-schema:Person) as ?actor_class)
  }
  UNION
  {
    ?actor a crm:E74_Group .
    BIND(IRI(mmm-schema:Organization) as ?actor_class)
  }
  UNION
  {
    ?actor a frbr:F44_Bibliographic_Agency .
    BIND(IRI(mmm-schema:Organization) as ?actor_class)
  }

  OPTIONAL {
    ?actor rdfs:label ?actor_label .
  }

  OPTIONAL {
    ?actor crm:P98i_was_born ?birth_event .
    ?birth_event rdfs:label ?birth_label .
    OPTIONAL {
      ?birth_event crm:P4_has_time-span ?birth_timespan .
      ?birth_timespan rdfs:label ?birth_timespan_label .
      OPTIONAL { ?birth_timespan crm:P82a_begin_of_the_begin ?birth_timespan_begin_begin }
      OPTIONAL { ?birth_timespan crm:P82b_end_of_the_end ?birth_timespan_end_end }
      BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bodley_", REPLACE(STR(?actor), "^.*\\/(.+)", "$1"), "_birth_timespan")) as ?birth_timespan_mmm)
    }
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", REPLACE(STR(?actor), "^.*\\/(.+)", "$1"), "_birth")) as ?birth_mmm)
  }

  OPTIONAL {
    ?actor crm:P100i_died_in ?death_event .
    ?death_event rdfs:label ?death_label .
    OPTIONAL {
      ?death_event crm:P4_has_time-span ?death_timespan .
      ?death_timespan rdfs:label ?death_timespan_label .
      OPTIONAL { ?death_timespan crm:P82a_begin_of_the_begin ?death_timespan_begin_begin }
      OPTIONAL { ?death_timespan crm:P82b_end_of_the_end ?death_timespan_end_end }
      BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bodley_", REPLACE(STR(?actor), "^.*\\/(.+)", "$1"), "_death_timespan")) as ?death_timespan_mmm)
    }
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", REPLACE(STR(?actor), "^.*\\/(.+)", "$1"), "_death")) as ?death_mmm)
  }

  OPTIONAL {
    ?actor crm:P1_is_identified_by ?appellation .
    ?appellation rdfs:label ?preflabel .
    OPTIONAL { ?appellation crm:P139_has_alternative_form/rdfs:label ?altlabel  }
  }

  OPTIONAL {
    ?actor owl:sameAs ?external_uri .
  }

  BIND(REPLACE(STR(?actor), "^.*\\/(.+)", "$1") as ?actor_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?actor_local_id)) as ?actor_mmm)
}
