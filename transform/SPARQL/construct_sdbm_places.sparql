PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

BASE <http://>

CONSTRUCT {
  ?place a ecrm:E53_Place ;
    skos:prefLabel ?name ;
    ecrm:P89_falls_within ?parent ;
    owl:sameAs ?authority_uri ;
    wgs:lat ?lat ;
    wgs:long ?long ;
    mmms:data_provider_url ?place_id ;
    dct:source mmms:SDBM .
}
WHERE {
  ?place_id a :places ;
            :places_name ?name .
  OPTIONAL {
    ?place_id :places_parent_id ?parent_id .
    BIND(IRI(CONCAT(STR(mmm:), "place/sdbm_", REPLACE(STR(?parent_id), "^(.*)place/(.*)$", "$2"))) as ?parent)
  }
  OPTIONAL {
    ?place_id :places_authority_source ?authority_source .
    ?place_id :places_authority_id ?authority_localname .
  }
  BIND(IRI(REPLACE(STR(?authority_source), " ", "") + STR("/") + STR(?authority_localname)) as ?_authority_uri) .
  BIND(IRI(REPLACE(STR(?_authority_uri), "http:///", "http://")) as ?authority_uri) .
  OPTIONAL {
    ?place_id :places_latitude ?lat_ .
    FILTER(STR(?lat_) != "")
    ?place_id :places_latitude ?lat .
  }
  OPTIONAL {
    ?place_id :places_longitude ?long_ .
    FILTER(STR(?long_) != "")
    ?place_id :places_longitude ?long .
  }

  BIND(REPLACE(STR(?place_id), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT(STR(mmm:), "place/sdbm_", STR(?place_local_id))) as ?place)
}
