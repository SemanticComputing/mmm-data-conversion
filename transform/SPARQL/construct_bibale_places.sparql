PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# TODO:
# - mint MMM URIs for resources

CONSTRUCT {
  ?bibale_place_uri a crm:E53_Place ;
    skos:prefLabel ?pref_label ;
    skos:altLabel ?combined_label ;
    owl:sameAs ?authority_uri ;
    # wgs:lat ?lat ;
    # wgs:long ?long ;
    # mmm-schema:data_provider_url ?bod_place_uri ;
    mmm-schema:place_type ?place_type ;
    mmm-schema:bibale_settlement ?settlement_label ;
    mmm-schema:bibale_region ?region_label ;
    mmm-schema:bibale_country ?country_label ;
    dct:source mmm-schema:Bibale .
}
WHERE
{
  ?bibale_place_uri a crm:E53_Place .
  ?bibale_place_uri crm:P88_consists_of ?child .
  OPTIONAL { ?bibale_place_uri owl:sameAs ?authority_uri . }
  OPTIONAL {
    ?bibale_place_uri crm:P88_consists_of ?settlement .
    ?settlement a bibale:type:settlement .
    OPTIONAL {
      ?settlement rdfs:label ?settlement_label .
      FILTER(?settlement_label != "")
    }
    BIND(bibale:type:settlement as ?place_type_settlement)
  }
  OPTIONAL {
    ?bibale_place_uri crm:P88_consists_of ?region .
    ?region a bibale:type:region .
    OPTIONAL {
      ?region rdfs:label ?region_label .
      FILTER(?region_label != "")
    }
    BIND(bibale:type:region as ?place_type_region)
  }
  OPTIONAL {
    ?bibale_place_uri crm:P88_consists_of ?country .
    ?country a bibale:type:country .
    OPTIONAL {
      ?country rdfs:label ?country_label .
      FILTER(?country_label != "")
    }
    BIND(bibale:type:country as ?place_type_country)
  }

  BIND(COALESCE(?place_type_settlement, ?place_type_region, ?place_type_country) as ?place_type)
  BIND(COALESCE(?settlement_label, ?region_label, ?country_label) as ?pref_label)
  BIND(CONCAT(COALESCE(CONCAT(?country_label, ", "), ""), COALESCE(CONCAT(?region_label, ", "), ""), COALESCE(CONCAT(?settlement_label), "")) as ?combined_label)
}
