PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/schema/mmm/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT {
  ?place_mmm a ecrm:E53_Place ;
    skos:prefLabel ?pref_label ;
    skos:altLabel ?combined_label ;
    owl:sameAs ?authority_uri ;
    # wgs:lat ?lat ;
    # wgs:long ?long ;
    # mmms:data_provider_url ?bod_place_uri ;
    mmms:place_type ?place_type_mmm ;
    mmms:bibale_settlement ?settlement_label ;
    mmms:bibale_region ?region_label ;
    mmms:bibale_country ?country_label ;
    dct:source mmms:Bibale .
}
WHERE
{
  ?bibale_place_uri a crm:E53_Place .
  ?bibale_place_uri crm:P88_consists_of ?child .
  OPTIONAL { ?bibale_place_uri owl:sameAs ?authority_uri . }
  OPTIONAL {
    ?bibale_place_uri crm:P88_consists_of ?settlement .
    ?settlement a bibale:type:settlement .
    OPTIONAL {
      ?settlement rdfs:label ?settlement_label .
      FILTER(?settlement_label != "")
    }
    BIND(bibale:type:settlement as ?place_type_settlement)
  }
  OPTIONAL {
    ?bibale_place_uri crm:P88_consists_of ?region .
    ?region a bibale:type:region .
    OPTIONAL {
      ?region rdfs:label ?region_label .
      FILTER(?region_label != "")
    }
    BIND(bibale:type:region as ?place_type_region)
  }
  OPTIONAL {
    ?bibale_place_uri crm:P88_consists_of ?country .
    ?country a bibale:type:country .
    OPTIONAL {
      ?country rdfs:label ?country_label .
      FILTER(?country_label != "")
    }
    BIND(bibale:type:country as ?place_type_country)
  }

  BIND(COALESCE(?place_type_settlement, ?place_type_region, ?place_type_country) as ?place_type)
  BIND(COALESCE(?settlement_label, ?region_label, ?country_label) as ?pref_label)
  BIND(CONCAT(COALESCE(CONCAT(?country_label, ", "), ""), COALESCE(CONCAT(?region_label, ", "), ""), COALESCE(CONCAT(?settlement_label), "")) as ?combined_label)

  BIND(REPLACE(STR(?bibale_place_uri), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?place_local_id, ":", "_"))) as ?place_mmm)

  BIND(REPLACE(STR(?place_type), "^.*\\/(.+)", "$1") as ?place_type_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/schema/mmm/bibale_", REPLACE(?place_type_local_id, ":", "_"))) as ?place_type_mmm)
}
