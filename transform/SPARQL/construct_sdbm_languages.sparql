PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>

CONSTRUCT {
  ?language_mmm a ecrm:E56_Language ;
    skos:prefLabel ?language_name ;
    mmms:data_provider_url ?language ;
    dct:source mmms:SDBM .
}
WHERE {
  ?language a :languages ;
       :languages_name ?language_name .

  BIND(REPLACE(STR(?language), "^.*\\/(.+)", "$1") as ?language_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/language/sdbm_", ?language_local_id)) as ?language_mmm)
} order by ?language_name
