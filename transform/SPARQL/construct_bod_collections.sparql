PREFIX type: <http://info.deepcarbon.net/schema/type#>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/mmm/schema/>
PREFIX mmm-actors: <http://ldf.fi/mmm/actor/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT {
  ?collection_mmm a ecrm:E78_Collection ;
    skos:prefLabel ?label ;
    crm:P46i_forms_part_of ?collection2_mmm ;
    mmms:collection_location <http://ldf.fi/mmm/place/bodley_place_7011931> ;
    dct:source mmms:Bodley ;
    .
}
#SELECT *
WHERE
{
  ?collection a crm:E78_Collection ;
              rdfs:label ?label_ .

  OPTIONAL { ?collection crm:P46i_forms_part_of ?collection2 }

  BIND(STR(?label_) as ?label)

  BIND(REPLACE(STR(?collection), "^.*\\/(.+)", "$1") as ?collection_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/collection/bodley_", ?collection_local_id)) as ?collection_mmm)

  BIND(REPLACE(STR(?collection2), "^.*\\/(.+)", "$1") as ?collection2_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/collection/bodley_", ?collection2_local_id)) as ?collection2_mmm)
}
