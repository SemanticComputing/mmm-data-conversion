PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbr: <http://www.cidoc-crm.org/frbr/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bod-roles: <https://medieval.bodleian.ox.ac.uk/catalog/authority/roles/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT
{
  ?provenance_event_mmm a mmm-schema:ManuscriptObservation ;
    skos:prefLabel ?label ;
    mmm-schema:observed_manuscript ?manifestation_singleton_mmm ;
    mmm-schema:observed_owner ?actor_mmm ;
    crm:P3_has_note ?note ;
    crm:P7_took_place_at ?place_mmm ;
    crm:P4_has_time-span ?timespan_mmm ;
    dct:source mmm-schema:Bodley ;
    .

  ?timespan_mmm crm:P82a_begin_of_the_begin ?timespan_bb ;
    crm:P82b_end_of_the_end ?timespan_ee ;
    # TODO: prefLabel
    .

  ?manifestation_singleton_mmm mmm-schema:phillipps_number ?phillipps ;
    .
}
WHERE
{
  ?provenance_event a crm:E5_Event .
  ?manifestation_singleton crm:P12i_was_present_at ?provenance_event .

  OPTIONAL { ?provenance_event rdfs:label ?label }
  OPTIONAL { ?provenance_event crm:P11_had_participant ?actor }
  OPTIONAL {
    ?provenance_event crm:P3_has_note ?note .
    OPTIONAL {
      ?provenance_event crm:P3_has_note ?note .
      FILTER CONTAINS(?note, "Phillipps")
      BIND(REPLACE(STR(?note), "^.*Phillipps.+MS[\\.\\| ]+(\\d{3,5}).*$", "$1", "s") as ?phillipps)
      FILTER(?phillipps != STR(?note))
    }
  }

  OPTIONAL { ?provenance_event crm:P7_took_place_at ?place }
  OPTIONAL {
    ?provenance_event crm:P4_has_time-span ?timespan .
    OPTIONAL { ?timespan crm:P82a_begin_of_the_begin ?timespan_bb }
    OPTIONAL { ?timespan crm:P82b_end_of_the_end ?timespan_ee }
  }

  BIND(REPLACE(STR(?manifestation_singleton), "^.*\\/(.+)", "$1") as ?manifestation_singleton_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/manifestation_singleton/bodley_", ?manifestation_singleton_local_id)) as ?manifestation_singleton_mmm)

  BIND(REPLACE(STR(?actor), "^.*\\/(.+)", "$1") as ?actor_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?actor_local_id)) as ?actor_mmm)

  BIND(REPLACE(STR(?place), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bodley_", REPLACE(?place_local_id, ":", "_"))) as ?place_mmm)

  BIND(REPLACE(STR(?timespan), "^.*\\/(.+)", "$1") as ?timespan_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bodley_", REPLACE(?timespan_local_id, ":", "_"))) as ?timespan_mmm)

  BIND(REPLACE(STR(?provenance_event), "^.*\\/(.+)", "$1") as ?provenance_event_local_id)
  BIND(IRI(CONCAT(STR(mmm:), "observation/bodley_", ?provenance_event_local_id)) as ?provenance_event_mmm)

}
