PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbr: <http://www.cidoc-crm.org/frbr/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bod-roles: <https://medieval.bodleian.ox.ac.uk/catalog/authority/roles/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# TODO: Mint MMM URIs

CONSTRUCT
{
  ?sub a mmm-schema:ManuscriptObservation ;
    skos:prefLabel ?label ;
    mmm-schema:observed_owner ?actor ;
    crm:P3_has_note ?note ;
    crm:P7_took_place_at ?place ;
    crm:P4_has_time-span ?timespan ;
    .

  ?timespan crm:P82a_begin_of_the_begin ?timespan_bb ;
    crm:P82b_end_of_the_end ?timespan_ee ;
    # TODO: prefLabel
    .

}
WHERE
{
  ?sub a crm:E5_Event . # PROVENANCE EVENT
  OPTIONAL { ?sub rdfs:label ?label }
  OPTIONAL { ?sub crm:P11_had_participant ?actor }
  OPTIONAL { ?sub crm:P3_has_note ?note }
  OPTIONAL {
    ?sub crm:P4_has_time-span ?timespan .
    OPTIONAL { ?timespan crm:P82a_begin_of_the_begin ?timespan_bb }
    OPTIONAL { ?timespan crm:P82b_end_of_the_end ?timespan_ee }
  }
  OPTIONAL { ?sub crm:P7_took_place_at ?place }
}
