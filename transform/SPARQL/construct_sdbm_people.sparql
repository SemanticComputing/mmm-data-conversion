PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>

CONSTRUCT {
  ?mmm_actor_uri a ?actor_class ;
    skos:prefLabel ?name ;
    mmm-schema:viaf_id ?viaf_id ;
    mmm-schema:person_place ?mmm_place ;  # Place/nationality
    mmm-schema:birth_date ?birth_date ;
    mmm-schema:death_date ?death_date ;
    mmm-schema:data_provider_url ?name_id ;
    dct:source mmm-schema:SDBM .
} WHERE {
  ?name_id a :names ;
           :names_name ?name ;
           :names_id ?person_local_id .
  FILTER(NOT EXISTS { ?name_id :names_deleted true })

  OPTIONAL {
    ?name_id :names_subtype "Personal" .
    BIND(IRI(mmm-schema:Person) as ?actor_class)
  }
  OPTIONAL {
    ?name_id :names_subtype "Corporate" .
    BIND(IRI(mmm-schema:Organization) as ?actor_class)
  }
  OPTIONAL { ?name_id :names_subtype ?name_type_literal . }
  OPTIONAL {
    ?name_id :names_viaf_id ?viaf_id .
    FILTER(STR(?viaf_id) != "")
  }
  OPTIONAL {
    ?name_id :names_startdate ?birth_date .
    FILTER(STR(?birth_date) != "")
  }
  OPTIONAL {
    ?name_id :names_enddate ?death_date .
    FILTER(STR(?death_date) != "")
  }

  OPTIONAL {
    ?name_place_id :name_places_name_id ?name_id ;
                   :name_places_place_id ?place_id ;
                   a :name_places .
    ?place_id :places_id ?place_local_id .
  }
  BIND(IRI(CONCAT(STR(mmm:), "places/", STR(?place_local_id))) as ?mmm_place)
  BIND(IRI(CONCAT(STR(mmm:), "actors/", STR(?person_local_id))) as ?mmm_actor_uri)
}
