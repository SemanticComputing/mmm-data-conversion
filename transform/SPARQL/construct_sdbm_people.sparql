PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# TODO: Format dates

CONSTRUCT {
  ?mmm_actor_uri a ?actor_class ;
    skos:prefLabel ?name ;
    owl:sameAs ?viaf_uri ;
    mmms:person_place ?mmm_place ;  # Place/nationality
    ecrm:P98i_was_born ?birth_mmm ;
    ecrm:P100i_died_in ?death_mmm ;
    mmms:data_provider_url ?name_id ;
    dct:source mmms:SDBM ;
    ecrm:P3_has_note ?other_info ;
    ecrm:P3_has_note ?name_type_literal ;
    .

  ?birth_mmm a ?birth_class ;
    skos:prefLabel ?birth_label ;
    ecrm:P4_has_time-span ?birth_timespan_mmm ;
    .

  ?birth_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?birth_timespan_label ;
    ecrm:P82a_begin_of_the_begin ?birth_begin_mmm ;
    ecrm:P82b_end_of_the_end ?birth_end_mmm ;
    .

  ?death_mmm a ?death_class ;
    skos:prefLabel ?death_label ;
    ecrm:P4_has_time-span ?death_timespan_mmm ;
    .

  ?death_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?death_timespan_label ;
    ecrm:P82a_begin_of_the_begin ?death_begin ;
    ecrm:P82b_end_of_the_end ?death_end ;
    .

}
WHERE
{
  ?name_id a :names ;
           :names_id ?person_local_id .
  FILTER(NOT EXISTS { ?name_id :names_deleted true })

  # LABEL
  OPTIONAL {
    ?name_id :names_name ?name
  }
  OPTIONAL {
    BIND("Unknown actor" as ?name)
  }

  # CLASS (PERSON / ORGANIZATION / ACTOR)
  OPTIONAL {
    ?name_id :names_subtype "Personal" .
    BIND(IRI(ecrm:E21_Person) as ?actor_class)
    BIND(ecrm:E67_Birth as ?birth_class)
    BIND(ecrm:E69_Death as ?death_class)
    BIND("Birth of " as ?birth_prefix)
    BIND("Death of " as ?death_prefix)
  }
  OPTIONAL {
    ?name_id :names_subtype "Corporate" .
    BIND(IRI(ecrm:E74_Group) as ?actor_class)
    BIND(ecrm:E66_Formation as ?birth_class)
    BIND(ecrm:E68_Dissolution as ?death_class)
    BIND("Formation of " as ?birth_prefix)
    BIND("Dissolution of " as ?death_prefix)
  }
  OPTIONAL {
    # Actors of unknown type
    BIND(IRI(ecrm:E39_Actor) as ?actor_class)
    BIND(ecrm:E63_Beginning_of_Existence as ?birth_class)
    BIND(ecrm:E64_End_of_Existence as ?death_class)
    BIND("Beginning of existence of " as ?birth_prefix)
    BIND("End of existence of " as ?death_prefix)
  }

  # NAME SUBTYPE (e.g. Personal, Corporate, Geographic)
  OPTIONAL {
    ?name_id :names_subtype ?name_type_literal .
    FILTER(STR(?name_type_literal) != "") .
  }

  # VIAF URI
  OPTIONAL {
    ?name_id :names_viaf_id ?viaf_id .
    FILTER(STR(?viaf_id) != "") .
    BIND(URI(CONCAT("https://viaf.org/viaf/", REPLACE(?viaf_id, " ", ""))) as ?viaf_uri)
  }

  # BIRTH DATE
  OPTIONAL {
    ?name_id :names_startdate ?birth_date .

    FILTER(?birth_date != "" && ?birth_date != "0")

    BIND(SUBSTR(?birth_date, 1, 4) as ?birth_year_)
    BIND(SUBSTR(?birth_date, 6, 2) as ?birth_month_)
    BIND(SUBSTR(?birth_date, 9) as ?birth_day_)

    BIND(IF(STRSTARTS(?birth_year_, "-"),
        CONCAT("-", SUBSTR("0000", STRLEN(?birth_year_)), SUBSTR(?birth_year_, 2, 4)),
        CONCAT(SUBSTR("0000", STRLEN(?birth_year_) + 1), ?birth_year_)) as ?birth_year_iso)

    OPTIONAL {
      FILTER (?birth_day_ = "00" && (?birth_month_ = "02"))
      BIND("01" as ?birth_begin_day_)
      BIND("28" as ?birth_end_day_)
    }
    OPTIONAL {
      FILTER (?birth_day_ = "00" && (?birth_month_ = "04" || ?birth_month_ = "06" || ?birth_month_ = "09" || ?birth_month_ = "11"))
      BIND("01" as ?birth_begin_day_)
      BIND("30" as ?birth_end_day_)
    }
    OPTIONAL {
      FILTER (?birth_month_ != "" && (!BOUND(?birth_day_) || ?birth_day_ = "00"))
      BIND("01" as ?birth_begin_day_)
      BIND("31" as ?birth_end_day_)
    }
    OPTIONAL {
      FILTER (?birth_year_ != "" && !BOUND(?birth_month_) && (!BOUND(?birth_day_) || ?birth_day_ = "00"))
      BIND("01" as ?birth_begin_month_)
      BIND("12" as ?birth_end_month_)
      BIND("01" as ?birth_begin_day_)
      BIND("31" as ?birth_end_day_)
    }
    BIND(COALESCE(?birth_begin_month_, ?birth_month_) as ?birth_begin_month)
    BIND(COALESCE(?birth_end_month_, ?birth_month_) as ?birth_end_month)
    BIND(COALESCE(?birth_begin_day_, ?birth_day_) as ?birth_begin_day)
    BIND(COALESCE(?birth_end_day_, ?birth_day_) as ?birth_end_day)

    BIND(?birth_year_iso + "-" + ?birth_begin_month + "-" + ?birth_begin_day as ?birth_begin)
    BIND(?birth_year_iso + "-" + ?birth_end_month + "-" + ?birth_end_day as ?birth_end)

    BIND(STRDT(?birth_begin, xsd:date) as ?birth_begin_mmm)
    BIND(STRDT(?birth_end, xsd:date) as ?birth_end_mmm)

    BIND(IF(?birth_begin = ?birth_end, ?birth_begin, IF(?birth_begin_month = ?birth_end_month, ?birth_begin + " - " + ?birth_end, ?birth_year_iso)) as ?birth_timespan_label)

    ?name_id :names_id ?person_local_id .
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/sdbm_", STR(?person_local_id), "_birth")) as ?birth_mmm)
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/sdbm_", STR(?person_local_id), "_birth_timespan")) as ?birth_timespan_mmm)
  }

  # DEATH DATE
  OPTIONAL {
    ?name_id :names_enddate ?death_date .
    FILTER(STR(?death_date) != "")

    BIND(SUBSTR(?death_date, 1, 4) as ?death_year_)
    BIND(SUBSTR(?death_date, 6, 2) as ?death_month_)
    BIND(SUBSTR(?death_date, 9) as ?death_day_)

    BIND(IF(STRSTARTS(?death_year_, "-"),
        CONCAT("-", SUBSTR("0000", STRLEN(?death_year_)), SUBSTR(?death_year_, 2, 4)),
        CONCAT(SUBSTR("0000", STRLEN(?death_year_) + 1), ?death_year_)) as ?death_year_iso)

    OPTIONAL {
      FILTER (?death_day_ = "00" && (?death_month_ = "02"))
      BIND("01" as ?death_begin_day_)
      BIND("28" as ?death_end_day_)
    }
    OPTIONAL {
      FILTER (?death_day_ = "00" && (?death_month_ = "04" || ?death_month_ = "06" || ?death_month_ = "09" || ?death_month_ = "11"))
      BIND("01" as ?death_begin_day_)
      BIND("30" as ?death_end_day_)
    }
    OPTIONAL {
      FILTER (?death_month_ != "" && (!BOUND(?death_day_) || ?death_day_ = "00"))
      BIND("01" as ?death_begin_day_)
      BIND("31" as ?death_end_day_)
    }
    OPTIONAL {
      FILTER (?death_year_ != "" && !BOUND(?death_month_) && (!BOUND(?death_day_) || ?death_day_ = "00"))
      BIND("01" as ?death_begin_month_)
      BIND("12" as ?death_end_month_)
      BIND("01" as ?death_begin_day_)
      BIND("31" as ?death_end_day_)
    }
    BIND(COALESCE(?death_begin_month_, ?death_month_) as ?death_begin_month)
    BIND(COALESCE(?death_end_month_, ?death_month_) as ?death_end_month)
    BIND(COALESCE(?death_begin_day_, ?death_day_) as ?death_begin_day)
    BIND(COALESCE(?death_end_day_, ?death_day_) as ?death_end_day)

    BIND(?death_year_iso + "-" + ?death_begin_month + "-" + ?death_begin_day as ?death_begin)
    BIND(?death_year_iso + "-" + ?death_end_month + "-" + ?death_end_day as ?death_end)

    BIND(STRDT(?death_begin, xsd:date) as ?death_begin_mmm)
    BIND(STRDT(?death_end, xsd:date) as ?death_end_mmm)

    BIND(IF(?death_begin = ?death_end, ?death_begin, IF(?death_begin_month = ?death_end_month, ?death_begin + " - " + ?death_end, ?death_year_iso)) as ?death_timespan_label)

    ?name_id :names_id ?person_local_id .
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/sdbm_", STR(?person_local_id), "_death")) as ?death_mmm)
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/sdbm_", STR(?person_local_id), "_death_timespan")) as ?death_timespan_mmm)
  }

  # OTHER INFO
  OPTIONAL {
    ?name_id :names_other_info ?other_info
    FILTER(STR(?other_info) != "") .
  }

  # ACTOR PLACE
  OPTIONAL {
    ?name_place_id :name_places_name_id ?name_id ;
                   :name_places_place_id ?place_id ;
                   a :name_places .
    ?place_id :places_id ?place_local_id .
  }

  BIND(?birth_prefix + ?name as ?birth_label)
  BIND(?death_prefix + ?name as ?death_label)

  BIND(IRI(CONCAT(STR(mmm:), "place/sdbm_", STR(?place_local_id))) as ?mmm_place)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", STR(?person_local_id))) as ?mmm_actor_uri)
}
