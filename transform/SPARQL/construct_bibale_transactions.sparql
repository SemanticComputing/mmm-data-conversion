PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX mmm-actors: <http://ldf.fi/mmm/actor/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# TODO: Get approximate time-span from associated persons life times?
# TODO: Add ownership helper resources to manuscript (and collection)


CONSTRUCT {
  ?transfer_mmm a ecrm:E10_Transfer_of_Custody ;
    ecrm:P30_transferred_custody_of ?manuscript_or_collection_mmm ;
    ecrm:P28_custody_surrendered_by ?old_owner ;
    ecrm:P29_custody_received_by ?new_owner_mmm ;
    ecrm:P7_took_place_at ?place_mmm ;
    ecrm:P4_has_time-span ?timespan_mmm ;
    .

  ?timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?timespan_label ;
    ecrm:P82a_begin_of_the_begin ?timespan_bb ;
    ecrm:P81a_end_of_the_begin ?timespan_eb ;
    ecrm:P81b_begin_of_the_end ?timespan_be ;
    ecrm:P82b_end_of_the_end ?timespan_ee ;
    .

}
WHERE
{
  ?transfer a crm:E10_Transfer_of_custody .

  OPTIONAL { ?transfer crm:P30_transferred_custody_of ?manuscript_or_collection }
  OPTIONAL { ?transfer crm:P28_custody_surrended_by ?old_owner }  # "surrended" in the data
  OPTIONAL { ?transfer crm:P29_custody_received_by ?new_owner }
  OPTIONAL { ?transfer crm:P7_took_place_at ?place }
  OPTIONAL {
    ?transfer crm:P4_has_time-span ?timespan .

    ?timespan rdfs:label ?timespan_label ;
      OPTIONAL { ?timespan crm:P82a_begin_of_the_begin ?timespan_bb }
      OPTIONAL { ?timespan crm:P81a_end_of_the_begin ?timespan_eb }
      OPTIONAL { ?timespan crm:P81b_begin_of_the_end ?timespan_be }
      OPTIONAL { ?timespan crm:P82b_end_of_the_end ?timespan_ee }
   }

  OPTIONAL {
    FILTER EXISTS { ?manuscript_or_collection a bibale:type:livre }
    BIND("http://ldf.fi/mmm/manifestation_singleton/bibale_" as ?manuscript_or_collection_prefix)
  }

  OPTIONAL {
    FILTER EXISTS { ?manuscript_or_collection a crm:E78_Collection }
    BIND("http://ldf.fi/mmm/collection/bibale_" as ?manuscript_or_collection_prefix)
  }

  # There are also other types of resources in the transfer of custody events,
  # which are now left out (provenance, source, binding)

  BIND(REPLACE(STR(?transfer), "^.*\\/(.+)", "$1") as ?transfer_local_id)
  BIND(IRI(CONCAT(STR(mmm:), "observation/", ?transfer_local_id)) as ?transfer_mmm)

  BIND(REPLACE(STR(?manuscript_or_collection), "^.*\\/(.+)", "$1") as ?manuscript_or_collection_local_id)
  BIND(IRI(CONCAT(?manuscript_or_collection_prefix, ?manuscript_or_collection_local_id)) as ?manuscript_or_collection_mmm)

  BIND(REPLACE(STR(?old_owner), "^.*\\/(.+)", "$1") as ?old_owner_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?old_owner_local_id)) as ?old_owner_mmm)
  BIND(REPLACE(STR(?new_owner), "^.*\\/(.+)", "$1") as ?new_owner_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?new_owner_local_id)) as ?new_owner_mmm)

  BIND(REPLACE(STR(?place), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?place_local_id, ":", "_"))) as ?place_mmm)

  BIND(REPLACE(STR(?timespan), "^.*\\/(.+)", "$1") as ?timespan_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?timespan_local_id, ":", "_"))) as ?timespan_mmm)

}
