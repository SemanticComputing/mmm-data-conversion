PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX mmm-actors: <http://ldf.fi/mmm/actor/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# TODO: Try to get approximate time-span
# TODO: Mint MMM URI for ?transfer

CONSTRUCT {
  ?transfer a crm:E10_Transfer_of_custody ;
    crm:P30_transferred_custody_of ?collection_mmm ;
    crm:P29_custody_received_by ?actor_mmm ;
    crm:P7_took_place_at ?place_mmm ;
    .
}
WHERE
{
  ?transfer a crm:E10_Transfer_of_custody ;
    crm:P30_transferred_custody_of ?collection ;
    crm:P29_custody_received_by ?actor ;
    crm:P7_took_place_at ?place .

  BIND(REPLACE(STR(?collection), "^.*\\/(.+)", "$1") as ?collection_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/manifestation_singleton/bibale_", ?collection_local_id)) as ?collection_mmm)

  BIND(REPLACE(STR(?actor), "^.*\\/(.+)", "$1") as ?actor_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id)) as ?actor_mmm)

  BIND(REPLACE(STR(?place), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?place_local_id, ":", "_"))) as ?place_mmm)
}
