PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX ecrm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX eecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>

CONSTRUCT {
  ?work_mmm a frbroo:F1_Work ;
    skos:prefLabel ?work_pref_label ;
    skos:altLabel ?work_alt_label ;
    mmm-schema:data_provider_url ?work ;
    dct:source mmm-schema:Bibale .

  ?work_conception_mmm a frbroo:F27_Work_Conception ;
    skos:prefLabel ?work_conception_pref_label ;
    frbroo:R16_initiated ?work_mmm ;
    mmm-schema:carried_out_by_as_author ?work_conception_author ;
    mmm-schema:carried_out_by_as_translator ?work_conception_translator ;
    mmm-schema:carried_out_by_as_editor ?work_conception_editor ;
    mmm-schema:carried_out_by_as_commissioner ?work_conception_commissioner ;
    mmm-schema:carried_out_by_as_recipient ?work_conception_recipient ;
    ecrm:P7_took_place_at ?place_mmm ;
    .
}
WHERE
{
  # WORK
  ?work a frbroo2:F1_Work .

  OPTIONAL { ?work rdfs:label ?work_pref_label }
  OPTIONAL { ?work crm:P102_has_title/rdfs:label ?work_alt_label }
  OPTIONAL {
    ?work frbroo2:R16i_was_initiated_by ?work_conception .
    ?work_conception a frbroo2:F27_Work_Conception .
    OPTIONAL { ?work_conception ecrm:P7_took_place_at ?work_place }
    OPTIONAL {
      ?work_conception ecrm:P01i_is_domain_of  [
        a ecrm:PC14_carried_out_by ;
        ecrm:P14.1_in_the_role_of [ ecrm:E55_Type  "Author" ] ;
        ecrm:P02_has_range ?work_conception_author ;
      ] ;
    }
    OPTIONAL {
      ?work_conception ecrm:P01i_is_domain_of  [
        a ecrm:PC14_carried_out_by ;
        ecrm:P14.1_in_the_role_of [ ecrm:E55_Type  "Translator" ] ;
        ecrm:P02_has_range ?work_conception_translator ;
      ] ;
    }
    OPTIONAL {
      ?work_conception ecrm:P01i_is_domain_of  [
        a ecrm:PC14_carried_out_by ;
        ecrm:P14.1_in_the_role_of [ ecrm:E55_Type  "Editor" ] ;
        ecrm:P02_has_range ?work_conception_editor ;
      ] ;
    }
    OPTIONAL {
      ?work_conception ecrm:P01i_is_domain_of  [
        a ecrm:PC14_carried_out_by ;
        ecrm:P14.1_in_the_role_of [ ecrm:E55_Type  "Commissioner" ] ;
        ecrm:P02_has_range ?work_conception_commissioner ;
      ] ;
    }
    OPTIONAL {
      ?work_conception ecrm:P01i_is_domain_of  [
        a ecrm:PC14_carried_out_by ;
        ecrm:P14.1_in_the_role_of [ ecrm:E55_Type  "Recipient" ] ;
        ecrm:P02_has_range ?work_conception_recipient ;
      ] ;
    }
  }
  BIND(REPLACE(STR(?work), "^.*\\/(.+)", "$1") as ?work_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/work/bibale_", ?work_local_id)) as ?work_mmm)

  BIND(REPLACE(STR(?work_conception), "^.*\\/(.+)", "$1") as ?work_conception_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/work_conception/bibale_", ?work_conception_local_id)) as ?work_conception_mmm)

  BIND(REPLACE(STR(?work_place), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?place_local_id, ":", "_"))) as ?place_mmm)
}
