PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>

CONSTRUCT
{
  ?acquisition_uri a ?event_type ;  # Acquisition or Observation
    skos:prefLabel ?cat_title ;
    ?event_property ?manifestation_singleton ;
    ?event_seller_or_holder_property ?seller ;
    ecrm:P29_custody_received_by ?buyer ;
    mmm-schema:carried_out_by_as_selling_agent ?selling_agent ;
    mmm-schema:data_provider_url ?entry ;
    ?event_place_property ?source_agent_location ;
    # mmm-schema:place_literal ?cat_location_literal ;
    ?event_timespan_property ?cat_date ;
    ecrm:P179_had_sales_price ?monetary_amount_uri ;
    ecrm:P70i_is_documented_in ?source_mmm .
    # dct:source mmm-schema:SDBM .

  ?monetary_amount_uri a ecrm:E97_Monetary_Amount ;
    ecrm:P180_has_currency ?sales_currency ;
    ecrm:P181_has_amount ?sales_price ;
    rdfs:comment ?sales_other_currency .
    # dct:source mmm-schema:SDBM .
}
WHERE
{
  ?entry a :entries .
  FILTER(NOT EXISTS { ?entry :entries_deprecated true })

  OPTIONAL {
    ?entry_manuscript :entry_manuscripts_entry_id ?entry .
    ?entry_manuscript a :entry_manuscripts .
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "is" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript .
      ?manuscript a :manuscripts .
      ?manuscript :manuscripts_id ?manuscript_id_int .
    }
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "partial" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript_parent .
      ?entry_manuscript :entry_manuscripts_entry_id ?entry_partial_uri .
      ?manuscript_parent a :manuscripts .
      ?manuscript_parent :manuscripts_id ?manuscript_parent_id_int .
    }
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "possible" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript .
      ?manuscript a :manuscripts .
      ?manuscript :manuscripts_id ?manuscript_id_int .
    }
  }

  ## TRANSACTION INFORMATION

  OPTIONAL {
    # ?entry :entries_transaction_type "sale" or "gift"
    ?sales_entry :sales_entry_id ?entry .
    ?sales_entry :sales_id ?sales_id .
    OPTIONAL { ?sales_entry :sales_price ?sales_price }
    OPTIONAL { ?sales_entry :sales_currency ?sales_currency }
    OPTIONAL { ?sales_entry :sales_other_currency ?sales_other_currency }
    OPTIONAL {
      ?sales_entry :sales_sold "Yes"
      BIND(crm:E10_Transfer_of_Custody as ?event_type)
      BIND(crm:P30_transferred_custody_of as ?event_property)
      BIND(crm:P7_took_place_at as ?event_place_property)
      BIND(crm:P4_has_time-span as ?event_timespan_property)
      BIND(crm:P28_custody_surrendered_by as ?event_seller_or_holder_property)
    }
    OPTIONAL {
      ?seller_agent :sale_agents_sale_id ?sales_entry .
      ?seller_agent :sale_agents_role "seller_or_holder" .
      ?seller_agent :sale_agents_agent_id/:names_id ?seller_id .
    }
    OPTIONAL {
      ?buyer_agent :sale_agents_sale_id ?sales_entry .
      ?buyer_agent :sale_agents_role "buyer" .
      ?buyer_agent :sale_agents_agent_id/:names_id ?buyer_id .
      BIND(crm:E10_Transfer_of_Custody as ?event_type)
      BIND(crm:P30_transferred_custody_of as ?event_property)
      BIND(crm:P7_took_place_at as ?event_place_property)
      BIND(crm:P4_has_time-span as ?event_timespan_property)
      BIND(crm:P28_custody_surrendered_by as ?event_seller_or_holder_property)
    }
    OPTIONAL {
      ?selling_agent_agent :sale_agents_sale_id ?sales_entry .
      ?selling_agent_agent :sale_agents_role "selling_agent" .
      ?selling_agent_agent :sale_agents_agent_id ?selling_agent_name .
      ?selling_agent_name :names_id ?selling_agent_id .
      # ?selling_agent_name (^:name_places_name_id)/:name_places_place_id/:places_id ?place_local_id .
      # BIND(IRI(CONCAT(STR(mmm:), "place/", STR(?place_local_id))) as ?selling_agent_location)
    }
  }
  OPTIONAL {
    BIND(mmm-schema:ManuscriptObservation as ?event_type)
    BIND(mmm-schema:observed_manuscript as ?event_property)
    BIND(mmm-schema:observed_location as ?event_place_property)
    BIND(mmm-schema:observed_time-span as ?event_timespan_property)
    BIND(mmm-schema:observed_owner as ?event_seller_or_holder_property)
  }

  ## SOURCE ##
  OPTIONAL {
    ?entry :entries_source_id ?entry_source .
    OPTIONAL{ ?entry_source :sources_id ?source_id }

    OPTIONAL {
      # Collection catalogs
      ?entry_source :sources_source_type_id <https://sdbm.library.upenn.edu/source_types/2> .
      BIND(CONCAT(STR(mmm:), "collection/sdbm_") as ?source_namespace)
      BIND(crm:P46i_forms_part_of as ?property_to_collection)

    }
    OPTIONAL {
      BIND(CONCAT(STR(mmm:), "source/sdbm_") as ?source_namespace)
    }

    # TODO: if source has no title, generate one for acquisition/observation prefLabel
    OPTIONAL{
      ?entry_source :sources_title ?cat_title
      FILTER(STR(?cat_title) != "")
    }
    OPTIONAL{
      ?entry_source :sources_date ?cat_date_raw
      FILTER(STR(?cat_date_raw) != "")
    }
    #OPTIONAL{ ?entry_source :sources_link ?cat_link }
    OPTIONAL{
      ?source_agent :source_agents_source_id ?entry_source .
      ?source_agent :source_agents_agent_id ?source_agent_name .
      ?source_agent_name (^:name_places_name_id)/:name_places_place_id/:places_id ?source_place_local_id .
      BIND(IRI(CONCAT(STR(mmm:), "place/sdbm_", STR(?source_place_local_id))) as ?source_agent_location)
    }
  }

  BIND(SUBSTR(?cat_date_raw, 1, 4) as ?cat_date_year)
  BIND(SUBSTR(?cat_date_raw, 5, 2) as ?cat_date_month)
  BIND(SUBSTR(?cat_date_raw, 7) as ?cat_date_day)
  BIND(?cat_date_year + "-" + ?cat_date_month + "-" + ?cat_date_day as ?cat_date)

  BIND(REPLACE(STR(?entry), "^.*\\/(.+)", "$1") as ?entry_id)
  BIND(REPLACE(STR(?entry_partial_uri), "^.*\\/(.+)", "$1") as ?entry_partial_id)

  # generate IRIs in LDF.fi namespace
  BIND(STR(?manuscript_id_int) AS ?manuscript_id_)
  BIND(COALESCE(?manuscript_id_, "part_" + STR(?entry_partial_id), "orphan_" + STR(?entry_id)) AS ?manuscript_id)
  BIND(IRI(CONCAT(STR(mmm:), "manifestation_singleton/sdbm_", ?manuscript_id)) as ?manifestation_singleton)
  BIND(IRI(CONCAT(STR(mmm:), "manifestation_singleton/sdbm_", STR(?manuscript_parent_id_int))) as ?parent_manifestation_singleton)
  BIND(IRI(CONCAT(?source_namespace, STR(?source_id))) as ?source_mmm)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", STR(?author_id))) as ?author)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", STR(?seller_id))) as ?seller)
  BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", STR(?buyer_id))) as ?buyer)
  BIND(IRI(CONCAT(STR(mmm:), "observation/sdbm_", ?entry_id)) as ?acquisition_uri)
  BIND(IRI(CONCAT(STR(mmm:), "monetary_amounts/sdbm_", ?sales_id)) as ?monetary_amount_uri)
}
