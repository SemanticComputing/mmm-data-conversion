PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/schema/mmm/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?mmm_source a mmms:Source , ?collection_class ;
    skos:prefLabel ?source_preflabel ;
    mmms:location_literal ?cat_location_literal ;
    mmms:institution_literal ?cat_institution ;
    mmms:source_date ?source_timespan_mmm ;
    mmms:external_url ?cat_link ;
    mmms:data_provider_url ?source ;
    mmms:source_agent ?mmm_source_actor ;
    mmms:source_type ?mmm_source_type ;
    ?location_property ?source_agent_location ;
    ecrm:P51_has_former_or_current_owner ?mmm_source_owner ;
    dct:source mmms:SDBM ;
    .

  ?source_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?source_date_label ;
    ecrm:P82a_begin_of_the_begin ?source_date_begin_mmm ;
    ecrm:P82b_end_of_the_end ?source_date_end_mmm ;
    dct:source mmms:SDBM ;
    .

  ?mmm_source_type a mmms:Source_Type ;
    skos:prefLabel ?source_type_label .
}
WHERE {
  ?source a :sources .
  FILTER(NOT EXISTS { ?source :sources_deleted true })

  OPTIONAL{
    ?source :sources_title ?source_title
    FILTER(STR(?source_title) != "")
  }

  OPTIONAL{
    ?source :sources_location ?cat_location_literal
    FILTER(STR(?cat_location_literal) != "")
  }

  OPTIONAL {
    # Collection catalogs
    ?source :sources_source_type_id <https://sdbm.library.upenn.edu/source_types/2> .
    BIND(ecrm:E78_Collection as ?collection_class)
    BIND(CONCAT(STR(mmm:), "collection/sdbm_") as ?source_namespace)
    BIND(mmms:collection_location as ?location_property)
  }

  OPTIONAL {
    BIND(CONCAT(STR(mmm:), "source/sdbm_") as ?source_namespace)
    BIND(mmms:source_location as ?location_property)
  }

  OPTIONAL {
    ?source :sources_source_type_id ?source_type .
    ?source_type :source_types_name ?source_type_local_name .
    ?source_type :source_types_display_name ?source_type_label
  }

  OPTIONAL{
    ?source :sources_date ?source_date_raw
    FILTER(STR(?source_date_raw) != "")
  }

  OPTIONAL{
    ?source :sources_location_institution ?cat_institution
    FILTER(STR(?cat_institution) != "")
  }

  OPTIONAL{
    ?source :sources_link ?cat_link_str
    FILTER(STR(?cat_link_str) != "")
    BIND(IF(STRSTARTS(?cat_link_str, "http://"), IRI(?cat_link_str), ?cat_link_str) as ?cat_link)
  }

  OPTIONAL{
    ?source_agent :source_agents_source_id ?source .
    ?source_agent :source_agents_agent_id ?source_agent_name .
    ?source_agent_name (^:name_places_name_id)/:name_places_place_id ?source_place .
    BIND(REPLACE(STR(?source_place), "^.*\\/(.+)", "$1") as ?source_place_id)
    BIND(IRI(CONCAT(STR(mmm:), "place/sdbm_", STR(?source_place_id))) as ?source_agent_location)

    OPTIONAL {
      ?source_agent :source_agents_role "institution" .
      ?source_agent :source_agents_agent_id ?source_agent_i .
      FILTER NOT EXISTS { ?source_agent_i :names_deleted true }
      BIND(REPLACE(STR(?source_agent_i), "^.*\\/(.+)", "$1") as ?source_agent_i_id)
      BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", STR(?source_agent_i_id))) as ?mmm_source_owner)
    }
    OPTIONAL {
      FILTER NOT EXISTS { ?source_agent :source_agents_role "institution" }
      ?source_agent :source_agents_agent_id ?source_agent_ .
      FILTER NOT EXISTS { ?source_agent_ :names_deleted true }
      BIND(REPLACE(STR(?source_agent_), "^.*\\/(.+)", "$1") as ?source_agent_id)
      BIND(IRI(CONCAT(STR(mmm:), "actor/sdbm_", STR(?source_agent_id))) as ?mmm_source_actor)
    }
  }

  OPTIONAL { ?source :sources_medium ?medium . }

  OPTIONAL { ?source :other_info ?other_info . }

  OPTIONAL { ?source :sources_author ?source_author . }

  # HANDLE DATES
  BIND(SUBSTR(?source_date_raw, 1, 4) as ?source_date_year)
  BIND(SUBSTR(?source_date_raw, 5, 2) as ?source_date_month_)
  BIND(SUBSTR(?source_date_raw, 7) as ?source_date_day_)

  OPTIONAL {
    FILTER (?source_date_month_ = "00")
    BIND("01" as ?source_date_begin_month_)
    BIND("12" as ?source_date_end_month_)
  }
  OPTIONAL {
    FILTER (?source_date_day_ = "00" && (?source_date_month_ = "02"))
    BIND("01" as ?source_date_begin_day_)
    BIND("28" as ?source_date_end_day_)
  }
  OPTIONAL {
    FILTER (?source_date_day_ = "00" && (?source_date_month_ = "04" || ?source_date_month_ = "06" || ?source_date_month_ = "09" || ?source_date_month_ = "11"))
    BIND("01" as ?source_date_begin_day_)
    BIND("30" as ?source_date_end_day_)
  }
  OPTIONAL {
    FILTER (?source_date_day_ = "00")
    BIND("01" as ?source_date_begin_day_)
    BIND("31" as ?source_date_end_day_)
  }
  BIND(COALESCE(?source_date_begin_month_, ?source_date_month_) as ?source_date_begin_month)
  BIND(COALESCE(?source_date_end_month_, ?source_date_month_) as ?source_date_end_month)
  BIND(COALESCE(?source_date_begin_day_, ?source_date_day_) as ?source_date_begin_day)
  BIND(COALESCE(?source_date_end_day_, ?source_date_day_) as ?source_date_end_day)

  BIND(?source_date_year + "-" + ?source_date_begin_month + "-" + ?source_date_begin_day as ?source_date_begin)
  BIND(?source_date_year + "-" + ?source_date_end_month + "-" + ?source_date_end_day as ?source_date_end)
  BIND(STRDT(?source_date_begin, xsd:date) as ?source_date_begin_mmm)
  BIND(STRDT(?source_date_end, xsd:date) as ?source_date_end_mmm)

  BIND(IF(?source_date_begin = ?source_date_end, ?source_date_begin, IF(?source_date_begin_month = ?source_date_end_month, ?source_date_begin + " - " + ?source_date_end, ?source_date_year)) as ?source_date_label)

  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/sdbm_sources_", ?source_date_raw)) as ?source_timespan_mmm)

  BIND(IRI(CONCAT(STR(mmms:), "source_types/", STR(?source_type_local_name))) as ?mmm_source_type)
  BIND(REPLACE(STR(?source), "^.*\\/(.+)", "$1") as ?source_local_id)
  BIND(IRI(CONCAT(?source_namespace, STR(?source_local_id))) as ?mmm_source)

  BIND(COALESCE(?source_title, "SDBM source " + STR(?source_local_id)) as ?source_preflabel)
}
