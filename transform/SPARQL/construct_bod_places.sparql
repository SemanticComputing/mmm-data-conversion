PREFIX frbr: <http://www.cidoc-crm.org/frbr/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/schema/mmm/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bod-roles: <https://medieval.bodleian.ox.ac.uk/catalog/authority/roles/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>

CONSTRUCT {
  ?place_mmm a ecrm:E53_Place ;
    skos:prefLabel ?pref_label ;
    skos:altLabel ?place_label ;
    skos:altLabel ?alt_label ;
    ecrm:P89_falls_within ?parent_place_mmm ;
    owl:sameAs  ?authority_uri ;
    wgs:lat ?lat ;
    wgs:long ?long ;
    mmms:data_provider_url ?bod_place_uri ;
    dct:source mmms:Bodley .
}
WHERE
{
  ?bod_place_uri a crm:E53_Place .
  OPTIONAL { ?bod_place_uri rdfs:label ?place_label }
  OPTIONAL {
    ?bod_place_uri crm:P1_is_identified_by ?appellation .
    ?appellation rdfs:label ?pref_label .
    OPTIONAL { ?appellation crm:P139_has_alternative_form/rdfs:label ?alt_label }
  }
  OPTIONAL { ?bod_place_uri crm:P89_falls_within ?parent_place }
  OPTIONAL {
    ?bod_place_uri crm:P89i_contains ?centroid .
    ?centroid crm:P168_place_is_defined_by ?coords .
    BIND(STRBEFORE(STRAFTER(?coords, "P("), ",") as ?lat)
    BIND(STRBEFORE(STRAFTER(?coords, ","), ")") as ?long)
  }
  OPTIONAL { ?bod_place_uri owl:sameAs ?authority_uri }
  FILTER(!STRENDS(STR(?bod_place_uri), "place-centroid"))

  BIND(REPLACE(STR(?bod_place_uri), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bodley_", REPLACE(?place_local_id, ":", "_"))) as ?place_mmm)

  # Fix malformed URIs in parent references
  BIND(REPLACE(REPLACE(STR(?parent_place), "^.*\\/(.+)", "$1"), "place_place_", "place_") as ?parent_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bodley_", REPLACE(?parent_place_local_id, ":", "_"))) as ?parent_place_mmm)
}
