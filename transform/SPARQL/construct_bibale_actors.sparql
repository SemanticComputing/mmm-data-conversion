PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmma: <http://ldf.fi/mmm/actor/>
PREFIX mmms: <http://ldf.fi/mmm/schema/>
PREFIX mmmt: <http://ldf.fi/mmm/time/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT {
  ?actor_mmm a ?actor_class ;
    skos:prefLabel ?pref_label ;
    skos:altLabel ?titulature ;
    mmms:data_provider_url ?bibale_actor_uri ;
    mmms:person_place ?place_mmm ;
    mmms:gender ?gender_literal ;
    ecrm:P98i_was_born ?birth_mmm ;
    ecrm:P100i_died_in ?death_mmm ;
    owl:sameAs ?external_uri ;
    mmms:biblissima_id ?biblissima_id ;
    mmms:religion ?religion_text, ?religious_order ;
    dct:source mmms:Bibale ;
    .

  ?birth_mmm a ?birth_class ;
    skos:prefLabel ?birth_label ;
    ecrm:P7_took_place_at ?birth_place_mmm ;
    ecrm:P4_has_time-span ?birth_time_mmm ;
    .

  ?birth_time_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?birth_time_label ;
    ecrm:P82a_begin_of_the_begin ?birth_time_bb ;
    ecrm:P81a_end_of_the_begin ?birth_time_eb ;
    ecrm:P81b_begin_of_the_end ?birth_time_be ;
    ecrm:P82b_end_of_the_end ?birth_time_ee ;
    .

  ?death_mmm a ?death_class ;
    skos:prefLabel ?death_label ;
    ecrm:P7_took_place_at ?death_place_mmm ;
    ecrm:P4_has_time-span ?death_time_mmm ;
    .

  ?death_time_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?death_time_label ;
    ecrm:P82a_begin_of_the_begin ?death_time_bb ;
    ecrm:P81a_end_of_the_begin ?death_time_eb ;
    ecrm:P81b_begin_of_the_end ?death_time_be ;
    ecrm:P82b_end_of_the_end ?death_time_ee ;
    .

  ?religion_event_mmm a ecrm:E7_Activity ;
    ecrm:P11_had_participant ?actor_mmm ;
    skos:prefLabel "Religion event" ;
    ecrm:P3_has_note ?religion_text, ?religious_order ;
    ecrm:P4_has_time-span ?religion_timespan_mmm ;
    .

  ?religion_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?religion_timespan_label ;
    ecrm:P82a_begin_of_the_begin ?religion_timespan_bb ;
    ecrm:P81a_end_of_the_begin ?religion_timespan_eb ;
    ecrm:P81b_begin_of_the_end ?religion_timespan_be ;
    ecrm:P82b_end_of_the_end ?religion_timespan_ee ;
    .

  ?activity_event_mmm a ecrm:E7_Activity ;
    ecrm:P11_had_participant ?actor_mmm ;
    skos:prefLabel "Activity event" ;
    ecrm:P4_has_time-span ?activity_timespan_mmm ;
    dct:source mmms:Bibale ;
    .

  ?activity_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?activity_timespan_label ;
    ecrm:P82a_begin_of_the_begin ?activity_timespan_bb ;
    ecrm:P81a_end_of_the_begin ?activity_timespan_eb ;
    ecrm:P81b_begin_of_the_end ?activity_timespan_be ;
    ecrm:P82b_end_of_the_end ?activity_timespan_ee ;
    .

}
WHERE
{
  ?bibale_actor_uri a crm:E39_Actor .
  OPTIONAL { ?bibale_actor_uri rdfs:label ?pref_label }
  OPTIONAL { ?bibale_actor_uri bibale:type:titulature ?titulature }
  OPTIONAL {
    # Gender data is messy, hence the property path and filter
    ?bibale_actor_uri bibale:type:person_sex/bibale:type:sex_sex+ ?gender_literal .
    FILTER(!ISIRI(?gender_literal))
  }

  # ORGANIZATION
  OPTIONAL {
    ?bibale_actor_uri bibale:type:person_type/rdfs:label* "morale" .
    BIND(ecrm:E66_Formation as ?birth_class)
    BIND(ecrm:E68_Dissolution as ?death_class)
    BIND(IRI(ecrm:E74_Group) as ?actor_class)
    OPTIONAL { ?bibale_actor_uri rdfs:label ?actor_pref_label }
    BIND("Formation of " + COALESCE(?actor_pref_label, "a group") as ?birth_label)
    BIND("Dissolution of " + COALESCE(?actor_pref_label, "a group") as ?death_label)
  }

  # PERSON
  OPTIONAL {
    ?bibale_actor_uri bibale:type:person_type/rdfs:label* "physique" .
    BIND(IRI(ecrm:E21_Person) as ?actor_class)
    BIND(ecrm:E67_Birth as ?birth_class)
    BIND(ecrm:E69_Death as ?death_class)
    OPTIONAL { ?bibale_actor_uri rdfs:label ?actor_pref_label }
    BIND("Birth of " + COALESCE(?actor_pref_label, "a person") as ?birth_label)
    BIND("Death of " + COALESCE(?actor_pref_label, "an actor") as ?death_label)
  }

  # ACTOR
  OPTIONAL {
    BIND(IRI(ecrm:E39_Actor) as ?actor_class)
    BIND(ecrm:E63_Beginning_of_Existence as ?birth_class)
    BIND(ecrm:E64_End_of_Existence as ?death_class)
    OPTIONAL { ?bibale_actor_uri rdfs:label ?actor_pref_label }
    BIND("Beginning of existence of " + COALESCE(?actor_pref_label, "an actor") as ?birth_label)
    BIND("End of existence of " + COALESCE(?actor_pref_label, "an actor") as ?death_label)
  }

  OPTIONAL {
    ?bibale_actor_uri crm:P53_has_former_or_current_location ?bibale_place_uri .
  }


  # BIRTH
  OPTIONAL {
    { ?bibale_actor_uri crm:P98i_was_born ?birth }
    UNION
    { ?birth crm:P98_brought_into_life ?bibale_actor_uri }

    OPTIONAL { ?bibale_actor_uri rdfs:label ?person_pref_label }
    OPTIONAL { ?birth crm:P7_took_place_at ?birth_place }
    OPTIONAL {
      ?birth crm:P4_has_time-span ?birth_time .

      OPTIONAL { ?birth_time crm:P82a_begin_of_the_begin ?birth_time_bb }
      OPTIONAL { ?birth_time crm:P81a_end_of_the_begin ?birth_time_eb }
      OPTIONAL { ?birth_time crm:P81b_begin_of_the_end ?birth_time_be }
      OPTIONAL { ?birth_time crm:P82b_end_of_the_end ?birth_time_ee }
      OPTIONAL { ?birth_time rdfs:label ?birth_time_label_ }

      BIND(SUBSTR(STR(?birth_time_bb), 6, 10) as ?birth_time_bb_month_day)
      BIND(SUBSTR(STR(?birth_time_ee), 6, 10) as ?birth_time_ee_month_day)
      BIND(
        IF(BOUND(?birth_time_bb) && BOUND(?birth_time_ee),
          IF(STR(?birth_time_bb) = STR(?birth_time_ee), STR(?birth_time_bb),
            IF(SUBSTR(STR(?birth_time_bb), 1, 4) = SUBSTR(STR(?birth_time_ee), 1, 4) && ?birth_time_bb_month_day = '01-01' && ?birth_time_ee_month_day = '12-31', SUBSTR(STR(?birth_time_bb), 1, 4),
              IF(?birth_time_bb_month_day = '01-01' && ?birth_time_ee_month_day = '12-31',
                CONCAT(SUBSTR(STR(?birth_time_bb), 1, 4), " - ", SUBSTR(STR(?birth_time_ee), 1, 4)),
                CONCAT(STR(?birth_time_bb), " - ", STR(?birth_time_ee))
              ))),
          ?birth_time_label_) as ?birth_time_label)
    }

    # Must mint URI based on actor ID to combine place and time-span
    BIND(REPLACE(STR(?bibale_actor_uri), "^.*[\\/:]([0-9]+)", "$1") as ?actor_local_id_)
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id_, "_birth")) as ?birth_mmm)
  }

  # DEATH
  OPTIONAL {
    { ?bibale_actor_uri crm:P100i_died_in ?death }
    UNION
    { ?death crm:P100_was_death_of ?bibale_actor_uri }

    OPTIONAL { ?bibale_actor_uri rdfs:label ?person_pref_label }
    OPTIONAL { ?death crm:P7_took_place_at ?death_place }
    OPTIONAL {
      ?death crm:P4_has_time-span ?death_time .
      OPTIONAL { ?death_time crm:P82a_begin_of_the_begin ?death_time_bb }
      OPTIONAL { ?death_time crm:P81a_end_of_the_begin ?death_time_eb }
      OPTIONAL { ?death_time crm:P81b_begin_of_the_end ?death_time_be }
      OPTIONAL { ?death_time crm:P82b_end_of_the_end ?death_time_ee }
      OPTIONAL { ?death_time rdfs:label ?death_time_label_ }

      BIND(SUBSTR(STR(?death_time_bb), 6, 10) as ?death_time_bb_month_day)
      BIND(SUBSTR(STR(?death_time_ee), 6, 10) as ?death_time_ee_month_day)
      BIND(
        IF(BOUND(?death_time_bb) && BOUND(?death_time_ee),
          IF(STR(?death_time_bb) = STR(?death_time_ee), STR(?death_time_bb),
            IF(SUBSTR(STR(?death_time_bb), 1, 4) = SUBSTR(STR(?death_time_ee), 1, 4) && ?death_time_bb_month_day = '01-01' && ?death_time_ee_month_day = '12-31', SUBSTR(STR(?death_time_bb), 1, 4),
              IF(?death_time_bb_month_day = '01-01' && ?death_time_ee_month_day = '12-31',
                CONCAT(SUBSTR(STR(?death_time_bb), 1, 4), " - ", SUBSTR(STR(?death_time_ee), 1, 4)),
                CONCAT(STR(?death_time_bb), " - ", STR(?death_time_ee))
              ))),
          ?death_time_label_) as ?death_time_label)
    }
    FILTER(BOUND(?death_time) || BOUND(?death_place))

    # Must mint URI based on actor ID to combine place and time-span
    BIND(REPLACE(STR(?bibale_actor_uri), "^.*[\\/:]([0-9]+)", "$1") as ?actor_local_id_)
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id_, "_death")) as ?death_mmm)
  }

  OPTIONAL {
    ?bibale_actor_uri owl:sameAs ?external_uri .
  }

  OPTIONAL {
    ?bibale_actor_uri bibale:type:id_biblissima ?biblissima_id .
  }

  # RELIGION
  OPTIONAL {
    ?bibale_actor_uri bibale:type:person_religion ?religion .
    OPTIONAL { ?religion bibale:type:person_religious_denomination ?religion_text }
    OPTIONAL { ?religion bibale:type:person_religious_order ?religious_order }
    OPTIONAL {
      ?religion bibale:type:religion_date ?religion_event .
      ?religion_event crm:P4_has_time-span ?religion_timespan .
      OPTIONAL { ?religion_timespan crm:P82a_begin_of_the_begin ?religion_timespan_bb }
      OPTIONAL { ?religion_timespan crm:P81a_end_of_the_begin ?religion_timespan_eb }
      OPTIONAL { ?religion_timespan crm:P81b_begin_of_the_end ?religion_timespan_be }
      OPTIONAL { ?religion_timespan crm:P82b_end_of_the_end ?religion_timespan_ee }
      OPTIONAL { ?religion_timespan rdfs:label ?religion_timespan_label_ }

      BIND(SUBSTR(STR(?religion_timespan_bb), 6, 10) as ?religion_timespan_bb_month_day)  # TODO: Use REGEX to take leading - into account
      BIND(SUBSTR(STR(?religion_timespan_ee), 6, 10) as ?religion_timespan_ee_month_day)  # TODO: Use REGEX to take leading - into account
      BIND(
        IF(BOUND(?religion_timespan_bb) && BOUND(?religion_timespan_ee),
          IF(STR(?religion_timespan_bb) = STR(?religion_timespan_ee), STR(?religion_timespan_bb),
            IF(SUBSTR(STR(?religion_timespan_bb), 1, 4) = SUBSTR(STR(?religion_timespan_ee), 1, 4) && ?religion_timespan_bb_month_day = '01-01' && ?religion_timespan_ee_month_day = '12-31', SUBSTR(STR(?religion_timespan_bb), 1, 4),
              IF(?religion_timespan_bb_month_day = '01-01' && ?religion_timespan_ee_month_day = '12-31',
                CONCAT(SUBSTR(STR(?religion_timespan_bb), 1, 4), " - ", SUBSTR(STR(?religion_timespan_ee), 1, 4)),
                CONCAT(STR(?religion_timespan_bb), " - ", STR(?religion_timespan_ee))
              ))),
          ?religion_timespan_label_) as ?religion_timespan_label)
    }
  }

  OPTIONAL {
    ?bibale_actor_uri bibale:type:activity_date ?activity .
    ?activity crm:P4_has_time-span ?activity_timespan .
    OPTIONAL { ?activity_timespan crm:P82a_begin_of_the_begin ?activity_timespan_bb }
    OPTIONAL { ?activity_timespan crm:P81a_end_of_the_begin ?activity_timespan_eb }
    OPTIONAL { ?activity_timespan crm:P81b_begin_of_the_end ?activity_timespan_be }
    OPTIONAL { ?activity_timespan crm:P82b_end_of_the_end ?activity_timespan_ee }
    OPTIONAL { ?activity_timespan rdfs:label ?activity_timespan_label_ }

    BIND(SUBSTR(STR(?activity_timespan_bb), 6, 10) as ?activity_timespan_bb_month_day)  # TODO: Use REGEX to take leading - into account
    BIND(SUBSTR(STR(?activity_timespan_ee), 6, 10) as ?activity_timespan_ee_month_day)  # TODO: Use REGEX to take leading - into account
    BIND(
      IF(BOUND(?activity_timespan_bb) && BOUND(?activity_timespan_ee),
        IF(STR(?activity_timespan_bb) = STR(?activity_timespan_ee), STR(?activity_timespan_bb),
          IF(SUBSTR(STR(?activity_timespan_bb), 1, 4) = SUBSTR(STR(?activity_timespan_ee), 1, 4) && ?activity_timespan_bb_month_day = '01-01' && ?activity_timespan_ee_month_day = '12-31', SUBSTR(STR(?activity_timespan_bb), 1, 4),
            IF(?activity_timespan_bb_month_day = '01-01' && ?activity_timespan_ee_month_day = '12-31',
              CONCAT(SUBSTR(STR(?activity_timespan_bb), 1, 4), " - ", SUBSTR(STR(?activity_timespan_ee), 1, 4)),
              CONCAT(STR(?activity_timespan_bb), " - ", STR(?activity_timespan_ee))
            ))),
        ?activity_timespan_label_) as ?activity_timespan_label)
  }

  BIND(REPLACE(STR(?bibale_actor_uri), "^.*\\/(.+)", "$1") as ?actor_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id)) as ?actor_mmm)

  BIND(REPLACE(STR(?bibale_place_uri), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?place_local_id, ":", "_"))) as ?place_mmm)

  BIND(REPLACE(STR(?birth_place), "^.*\\/(.+)", "$1") as ?birth_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?birth_place_local_id, ":", "_"))) as ?birth_place_mmm)
  BIND(REPLACE(STR(?birth_time), "^.*\\/(.+)", "$1") as ?birth_time_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?birth_time_local_id, ":", "_"))) as ?birth_time_mmm)

  BIND(REPLACE(STR(?death_place), "^.*\\/(.+)", "$1") as ?death_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?death_place_local_id, ":", "_"))) as ?death_place_mmm)
  BIND(REPLACE(STR(?death_time), "^.*\\/(.+)", "$1") as ?death_time_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?death_time_local_id, ":", "_"))) as ?death_time_mmm)

  BIND(REPLACE(STR(?religion_event), "^.*[\\/:]([0-9]+)", "$1") as ?religion_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_religion_event_", ?religion_local_id)) as ?religion_event_mmm)
  BIND(REPLACE(STR(?religion_timespan), "^.*[\\/:]([0-9]+)", "$1") as ?religion_timespan_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_religion_event_", ?religion_timespan_local_id)) as ?religion_timespan_mmm)

  BIND(REPLACE(STR(?activity), "^.*[\\/:]([0-9]+)", "$1") as ?activity_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_activity_event_", ?activity_local_id)) as ?activity_event_mmm)
  BIND(REPLACE(STR(?activity_timespan), "^.*[\\/:]([0-9]+)", "$1") as ?activity_timespan_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_activity_event_", ?activity_timespan_local_id)) as ?activity_timespan_mmm)

}
