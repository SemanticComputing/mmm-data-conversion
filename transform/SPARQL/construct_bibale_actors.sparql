PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmma: <http://ldf.fi/mmm/actor/>
PREFIX mmms: <http://ldf.fi/mmm/schema/>
PREFIX mmmt: <http://ldf.fi/mmm/time/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# TODO: Use E66_Formation, E68_Dissolution, E63_Beginning_of_Existence, E64_End_of_Existence

CONSTRUCT {
  ?actor_mmm a ?actor_class ;
    skos:prefLabel ?pref_label ;
    skos:altLabel ?titulature ;
    mmms:data_provider_url ?bibale_actor_uri ;
    mmms:person_place ?place_mmm ;
    mmms:gender ?gender_literal ;
    ecrm:P98i_was_born ?birth_mmm ;
    ecrm:P100i_died_in ?death_mmm ;
    owl:sameAs ?external_uri ;
    mmms:biblissima_id ?biblissima_id ;
    mmms:religion ?religion_text, ?religious_order ;
    dct:source mmms:Bibale .

  ?birth_mmm a ?birth_class ;
    skos:prefLabel ?birth_label ;
    ecrm:P7_took_place_at ?birth_place_mmm ;
    ecrm:P4_has_time-span ?birth_time_mmm ;
    .

  ?birth_time_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?birth_time_label ;
    ecrm:P82a_begin_of_the_begin ?birth_time_bb ;
    ecrm:P81a_end_of_the_begin ?birth_time_eb ;
    ecrm:P81b_begin_of_the_end ?birth_time_be ;
    ecrm:P82b_end_of_the_end ?birth_time_ee ;
    .

  ?death_mmm a ?death_class ;
    skos:prefLabel ?death_label ;
    ecrm:P7_took_place_at ?death_place_mmm ;
    ecrm:P4_has_time-span ?death_time_mmm ;
    .

  ?death_time_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?death_time_label ;
    ecrm:P82a_begin_of_the_begin ?death_time_bb ;
    ecrm:P81a_end_of_the_begin ?death_time_eb ;
    ecrm:P81b_begin_of_the_end ?death_time_be ;
    ecrm:P82b_end_of_the_end ?death_time_ee ;
    .

  ?religion_event_mmm a ecrm:E7_Activity ;
    ecrm:P11_had_participant ?actor_mmm ;
    skos:prefLabel "Religion event" ;
    ecrm:P3_has_note ?religion_text, ?religious_order ;
    ecrm:P4_has_time-span ?religion_timespan_mmm ;
    .

  ?religion_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?religion_timespan_label ;
    ecrm:P82a_begin_of_the_begin ?religion_bb ;
    ecrm:P81a_end_of_the_begin ?religion_eb ;
    ecrm:P81b_begin_of_the_end ?religion_be ;
    ecrm:P82b_end_of_the_end ?religion_ee ;
    .

  ?activity_event_mmm a ecrm:E7_Activity ;
    ecrm:P11_had_participant ?actor_mmm ;
    skos:prefLabel "Activity event" ;
    ecrm:P4_has_time-span ?activity_timespan_mmm ;
    .

  ?activity_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?activity_timespan_label ;
    ecrm:P82a_begin_of_the_begin ?activity_bb ;
    ecrm:P81a_end_of_the_begin ?activity_eb ;
    ecrm:P81b_begin_of_the_end ?activity_be ;
    ecrm:P82b_end_of_the_end ?activity_ee ;
    .

}
WHERE
{
  ?bibale_actor_uri a crm:E39_Actor .
  OPTIONAL { ?bibale_actor_uri rdfs:label ?pref_label }
  OPTIONAL { ?bibale_actor_uri bibale:type:titulature ?titulature }
  OPTIONAL {
    # Gender data is messy, hence the property path and filter
    ?bibale_actor_uri bibale:type:person_sex/bibale:type:sex_sex+ ?gender_literal .
    FILTER(!ISIRI(?gender_literal))
  }

  # ORGANIZATION
  OPTIONAL {
    ?bibale_actor_uri bibale:type:person_type/rdfs:label* "morale" .
    BIND(IRI(ecrm:E74_Group) as ?actor_class)
  }

  # PERSON
  OPTIONAL {
    BIND(IRI(ecrm:E21_Person) as ?actor_class)
  }

  OPTIONAL {
    ?bibale_actor_uri crm:P53_has_former_or_current_location ?bibale_place_uri .
  }


  # BIRTH
  OPTIONAL {
    { ?bibale_actor_uri crm:P98i_was_born ?birth }
    UNION
    { ?birth crm:P98_brought_into_life ?bibale_actor_uri }

    OPTIONAL { ?bibale_actor_uri rdfs:label ?person_pref_label }
    OPTIONAL { ?birth crm:P7_took_place_at ?birth_place }
    OPTIONAL {
      ?birth crm:P4_has_time-span ?birth_time .
      ?birth_time rdfs:label ?birth_time_label .
      OPTIONAL { ?birth_time crm:P82a_begin_of_the_begin ?birth_time_bb }
      OPTIONAL { ?birth_time crm:P81a_end_of_the_begin ?birth_time_eb }
      OPTIONAL { ?birth_time crm:P81b_begin_of_the_end ?birth_time_be }
      OPTIONAL { ?birth_time crm:P82b_end_of_the_end ?birth_time_ee }
    }
    BIND("Birth of " + COALESCE(?person_pref_label, "an actor") as ?birth_label)
    BIND(ecrm:E67_Birth as ?birth_class)

    # Must mint URI based on actor ID to combine place and time-span
    BIND(REPLACE(STR(?bibale_actor_uri), "^.*[\\/:]([0-9]+)", "$1") as ?actor_local_id_)
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id_, "_birth")) as ?birth_mmm)
  }

  # DEATH
  OPTIONAL {
    { ?bibale_actor_uri crm:P100i_died_in ?death }
    UNION
    { ?death crm:P100_was_death_of ?bibale_actor_uri }

    OPTIONAL { ?bibale_actor_uri rdfs:label ?person_pref_label }
    OPTIONAL { ?death crm:P7_took_place_at ?death_place }
    OPTIONAL {
      ?death crm:P4_has_time-span ?death_time .
      ?death_time rdfs:label ?death_time_label .
      OPTIONAL { ?death_time crm:P82a_begin_of_the_begin ?death_time_bb }
      OPTIONAL { ?death_time crm:P81a_end_of_the_begin ?death_time_eb }
      OPTIONAL { ?death_time crm:P81b_begin_of_the_end ?death_time_be }
      OPTIONAL { ?death_time crm:P82b_end_of_the_end ?death_time_ee }
    }
    FILTER(BOUND(?death_time) || BOUND(?death_place))
    BIND("Death of " + COALESCE(?person_pref_label, "an actor") as ?death_label)
    BIND(ecrm:E69_Death as ?death_class)

    # Must mint URI based on actor ID to combine place and time-span
    BIND(REPLACE(STR(?bibale_actor_uri), "^.*[\\/:]([0-9]+)", "$1") as ?actor_local_id_)
    BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id_, "_death")) as ?death_mmm)
  }

  OPTIONAL {
    ?bibale_actor_uri owl:sameAs ?external_uri .
  }

  OPTIONAL {
    ?bibale_actor_uri bibale:type:id_biblissima ?biblissima_id .
  }

  # RELIGION
  OPTIONAL {
    ?bibale_actor_uri bibale:type:person_religion ?religion .
    OPTIONAL { ?religion bibale:type:person_religious_denomination ?religion_text }
    OPTIONAL { ?religion bibale:type:person_religious_order ?religious_order }
    OPTIONAL {
      ?religion bibale:type:religion_date ?religion_event .
      ?religion_event crm:P4_has_time-span ?religion_timespan .
      ?religion_timespan rdfs:label ?religion_timespan_label .
      OPTIONAL { ?religion_timespan crm:P82a_begin_of_the_begin ?religion_bb }
      OPTIONAL { ?religion_timespan crm:P81a_end_of_the_begin ?religion_eb }
      OPTIONAL { ?religion_timespan crm:P81b_begin_of_the_end ?religion_be }
      OPTIONAL { ?religion_timespan crm:P82b_end_of_the_end ?religion_ee }
    }
  }

  OPTIONAL {
    ?bibale_actor_uri bibale:type:activity_date ?activity .
    ?activity crm:P4_has_time-span ?activity_timespan .
    ?activity_timespan rdfs:label ?activity_timespan_label .
    OPTIONAL { ?activity_timespan crm:P82a_begin_of_the_begin ?activity_bb }
    OPTIONAL { ?activity_timespan crm:P81a_end_of_the_begin ?activity_eb }
    OPTIONAL { ?activity_timespan crm:P81b_begin_of_the_end ?activity_be }
    OPTIONAL { ?activity_timespan crm:P82b_end_of_the_end ?activity_ee }
  }

  BIND(REPLACE(STR(?bibale_actor_uri), "^.*\\/(.+)", "$1") as ?actor_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id)) as ?actor_mmm)

  BIND(REPLACE(STR(?bibale_place_uri), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?place_local_id, ":", "_"))) as ?place_mmm)

  BIND(REPLACE(STR(?birth_place), "^.*\\/(.+)", "$1") as ?birth_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?birth_place_local_id, ":", "_"))) as ?birth_place_mmm)
  BIND(REPLACE(STR(?birth_time), "^.*\\/(.+)", "$1") as ?birth_time_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?birth_time_local_id, ":", "_"))) as ?birth_time_mmm)

  BIND(REPLACE(STR(?death_place), "^.*\\/(.+)", "$1") as ?death_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?death_place_local_id, ":", "_"))) as ?death_place_mmm)
  BIND(REPLACE(STR(?death_time), "^.*\\/(.+)", "$1") as ?death_time_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?death_time_local_id, ":", "_"))) as ?death_time_mmm)

  BIND(REPLACE(STR(?religion_event), "^.*[\\/:]([0-9]+)", "$1") as ?religion_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_religion_event_", ?religion_local_id)) as ?religion_event_mmm)
  BIND(REPLACE(STR(?religion_timespan), "^.*[\\/:]([0-9]+)", "$1") as ?religion_timespan_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_religion_event_", ?religion_timespan_local_id)) as ?religion_timespan_mmm)

  BIND(REPLACE(STR(?activity), "^.*[\\/:]([0-9]+)", "$1") as ?activity_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_activity_event_", ?activity_local_id)) as ?activity_event_mmm)
  BIND(REPLACE(STR(?activity_timespan), "^.*[\\/:]([0-9]+)", "$1") as ?activity_timespan_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_activity_event_", ?activity_timespan_local_id)) as ?activity_timespan_mmm)

}
