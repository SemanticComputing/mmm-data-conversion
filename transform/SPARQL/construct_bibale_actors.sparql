PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX mmm-actors: <http://ldf.fi/mmm/actor/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT {
  ?actor_mmm a ?actor_class ;
    skos:prefLabel ?pref_label ;
    mmm-schema:data_provider_url ?bibale_actor_uri ;
    mmm-schema:person_place ?place_mmm ;
    mmm-schema:gender ?gender_literal ;
    crm:P98i_was_born ?birth_mmm ;
    crm:P100i_died_in ?death_mmm ;
    dct:source mmm-schema:Bibale .

  ?birth_mmm a ?birth_class ;
    skos:prefLabel ?birth_label ;
    crm:P7_took_place_at ?birth_place_mmm ;
    crm:P4_has_time-span ?birth_time_mmm ;
    .

  ?death_mmm a ?death_class ;
    skos:prefLabel ?death_label ;
    crm:P7_took_place_at ?death_place_mmm ;
    crm:P4_has_time-span ?death_time_mmm ;
    .

  ?birth_time_mmm a crm:E52_Time-Span ;
    skos:prefLabel ?birth_time_label ;
    crm:P82a_begin_of_the_begin ?birth_time_bb ;
    crm:P81a_end_of_the_begin ?birth_time_eb ;
    crm:P81b_begin_of_the_end ?birth_time_be ;
    crm:P82b_end_of_the_end ?birth_time_ee ;
    .

  ?death_time_mmm a crm:E52_Time-Span ;
    skos:prefLabel ?death_time_label ;
    crm:P82a_begin_of_the_begin ?death_time_bb ;
    crm:P81a_end_of_the_begin ?death_time_eb ;
    crm:P81b_begin_of_the_end ?death_time_be ;
    crm:P82b_end_of_the_end ?death_time_ee ;
    .
}
WHERE
{
  ?bibale_actor_uri a crm:E39_Actor .
  OPTIONAL { ?bibale_actor_uri rdfs:label ?pref_label }
  OPTIONAL {
    # Gender data is messy, hence the property path and filter
    ?bibale_actor_uri bibale:type:person_sex/bibale:type:sex_sex+ ?gender_literal .
    FILTER(!ISIRI(?gender_literal))
  }

  OPTIONAL {
    # ORGANIZATION
    ?bibale_actor_uri bibale:type:person_type/rdfs:label* "morale" .
    BIND(IRI(mmm-schema:Organization) as ?actor_class)
  }

  OPTIONAL {
    BIND(IRI(mmm-schema:Person) as ?actor_class)
  }

  OPTIONAL {
    ?bibale_actor_uri crm:P53_has_former_or_current_location ?bibale_place_uri .
  }


  OPTIONAL {
    { ?bibale_actor_uri crm:P98i_was_born ?birth }
    UNION
    { ?birth crm:P98_brought_into_life ?bibale_actor_uri }

    OPTIONAL { ?bibale_actor_uri rdfs:label ?person_pref_label }
    OPTIONAL { ?birth crm:P7_took_place_at ?birth_place }
    OPTIONAL {
      ?birth crm:P4_has_time-span ?birth_time .
      ?birth_time rdfs:label ?birth_time_label ;
        OPTIONAL { ?birth_time crm:P82a_begin_of_the_begin ?birth_time_bb }
        OPTIONAL { ?birth_time crm:P81a_end_of_the_begin ?birth_time_eb }
        OPTIONAL { ?birth_time crm:P81b_begin_of_the_end ?birth_time_be }
        OPTIONAL { ?birth_time crm:P82b_end_of_the_end ?birth_time_ee }
     }
    BIND("Birth of " + COALESCE(?person_pref_label, "an actor") as ?birth_label)
    BIND(crm:E67_Birth as ?birth_class)
  }

  OPTIONAL {
    { ?bibale_actor_uri crm:P100i_died_in ?death }
    UNION
    { ?death crm:P100_was_death_of ?bibale_actor_uri }

    OPTIONAL { ?bibale_actor_uri rdfs:label ?person_pref_label }
    OPTIONAL { ?death crm:P7_took_place_at ?death_place }
    OPTIONAL {
      ?death crm:P4_has_time-span ?death_time .
      ?death_time rdfs:label ?death_time_label ;
        OPTIONAL { ?death_time crm:P82a_begin_of_the_begin ?death_time_bb }
        OPTIONAL { ?death_time crm:P81a_end_of_the_begin ?death_time_eb }
        OPTIONAL { ?death_time crm:P81b_begin_of_the_end ?death_time_be }
        OPTIONAL { ?death_time crm:P82b_end_of_the_end ?death_time_ee }
    }
    FILTER(BOUND(?death_time) || BOUND(?death_place))
    BIND("Death of " + COALESCE(?person_pref_label, "an actor") as ?death_label)
    BIND(crm:E69_Death as ?death_class)
  }

  BIND(REPLACE(STR(?bibale_actor_uri), "^.*\\/(.+)", "$1") as ?actor_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id)) as ?actor_mmm)

  BIND(REPLACE(STR(?bibale_place_uri), "^.*\\/(.+)", "$1") as ?place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?place_local_id, ":", "_"))) as ?place_mmm)

  BIND(REPLACE(STR(?birth), "^.*\\/(.+)", "$1") as ?birth_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id, "_birth")) as ?birth_mmm)
  BIND(REPLACE(STR(?birth_place), "^.*\\/(.+)", "$1") as ?birth_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?birth_place_local_id, ":", "_"))) as ?birth_place_mmm)
  BIND(REPLACE(STR(?birth_time), "^.*\\/(.+)", "$1") as ?birth_time_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?birth_time_local_id, ":", "_"))) as ?birth_time_mmm)

  BIND(REPLACE(STR(?death), "^.*\\/(.+)", "$1") as ?death_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?actor_local_id, "_death")) as ?death_mmm)
  BIND(REPLACE(STR(?death_place), "^.*\\/(.+)", "$1") as ?death_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?death_place_local_id, ":", "_"))) as ?death_place_mmm)
  BIND(REPLACE(STR(?death_time), "^.*\\/(.+)", "$1") as ?death_time_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?death_time_local_id, ":", "_"))) as ?death_time_mmm)
}
