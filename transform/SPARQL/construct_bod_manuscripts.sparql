PREFIX frbr: <http://www.cidoc-crm.org/frbr/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmms: <http://ldf.fi/schema/mmm/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bod-roles: <https://medieval.bodleian.ox.ac.uk/catalog/authority/roles/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# TODO: crm:P12i_was_present_at, crm:P46i_forms_part_of

CONSTRUCT {
  ?production_mmm a ecrm:E12_Production ;
    ecrm:P108_has_produced ?manifestation_singleton_mmm ;
    ecrm:P4_has_time-span ?production_timespan_mmm ;
    ecrm:P7_took_place_at ?production_place_mmm ;
    mmms:carried_out_by_as_artist ?production_artist_mmm ;
    mmms:carried_out_by_as_author ?production_author_mmm ;
    mmms:carried_out_by_as_binder ?production_binder_mmm ;
    mmms:carried_out_by_as_commissioner ?production_commissioner_mmm ;
    mmms:carried_out_by_as_dedicatee ?production_dedicatee_mmm ;
    mmms:carried_out_by_as_formerOwner ?production_formerOwner_mmm ;
    mmms:carried_out_by_as_patron ?production_patron_mmm ;
    mmms:carried_out_by_as_scribe ?production_scribe_mmm ;
    mmms:carried_out_by_as_signer ?production_signer_mmm ;
    skos:prefLabel ?production_label ;
    dct:source mmms:Bodley ;
    .

  ?production_timespan_mmm a ecrm:E52_Time-Span ;
    skos:prefLabel ?production_timespan_label ;
    ecrm:P82a_begin_of_the_begin ?production_timespan_begin_mmm ;
    ecrm:P82b_end_of_the_end ?production_timespan_end_mmm ;
    dct:source mmms:Bodley ;
    .

  ?manifestation_singleton_mmm a frbroo:F4_Manifestation_Singleton ;
    skos:prefLabel ?manifestation_singleton_preflabel ;
    ecrm:P128_carries ?expression_mmm ;
    ecrm:P52_has_current_owner ?prov_agent_mmm ;
    ecrm:P45_consists_of ?material_uri_mmm ;
    mmms:data_provider_url ?manifestation_singleton ;
    mmms:manuscript_author ?work_conception_author_mmm ;  # shortcut
    mmms:manuscript_author ?production_author_mmm ;  # shortcut
    mmms:manuscript_work ?work_mmm ;  # shortcut
    mmms:shelfmark_buchanan ?shelfmark_buchanan ;
    mmms:shelfmark_christ_church ?shelfmark_christ_church ;
    mmms:shelfmark_barocci ?shelfmark_barocci ;
    ecrm:P46i_forms_part_of ?collection_mmm ;
    ecrm:P46_is_composed_of ?child_manifestation_singleton_mmm ;
    dct:source mmms:Bodley ;
    .

  ?child_manifestation_singleton_mmm a frbroo:F4_Manifestation_Singleton ;
    dct:source mmms:Bodley ;
    .

  ?material_uri_mmm a ecrm:E57_Material ;
    # skos:prefLabel ?material_label ;
    owl:sameAs ?material_uri ;  # AAT link
    .

}
#SELECT DISTINCT ?manifestation_singleton ?manifestation_singleton_preflabel ?shelfmark_buchanan ?shelfmark_christ_church ?shelfmark_barocci
WHERE
{
  {
    ?manifestation_singleton a frbr:F4_Manifestation_Singleton .
  } UNION {
    ?manifestation_singleton a crm:E24_Physical_Man-Made_Thing .
  }

  ?manifestation_singleton rdfs:label ?manifestation_singleton_preflabel .

  OPTIONAL { ?manifestation_singleton crm:P52_has_current_owner ?prov_agent }

  OPTIONAL { ?manifestation_singleton crm:P46_is_composed_of ?child_manifestation_singleton }

  # EXPRESSION / WORK
  OPTIONAL {
    ?manifestation_singleton frbr:R42_is_representative_manifestation_singleton_for/frbr:R5_has_component? ?expression .
    OPTIONAL {
      ?expression frbr:R3i_realises ?work .
      ?work frbr:R16i_was_initiated_by ?work_conception .
      ?work_conception crm:P14_carried_out_by ?work_conception_author .
    }
  }

  # PRODUCTION
  OPTIONAL {
    ?manifestation_singleton crm:P108i_was_produced_by ?production .
    OPTIONAL {
      ?production rdfs:label ?production_label_ .
      BIND(STR(?production_label_) as ?production_label)
    }
    OPTIONAL {
      ?production crm:P4_has_time-span ?production_timespan .
      ?production_timespan crm:P82a_begin_of_the_begin ?production_timespan_begin .
      ?production_timespan crm:P82b_end_of_the_end ?production_timespan_end .

      BIND(STRDT(SUBSTR(STR(?production_timespan_begin), 0, 11), xsd:date) as ?production_timespan_begin_mmm)
      BIND(STRDT(SUBSTR(STR(?production_timespan_end), 0, 11), xsd:date) as ?production_timespan_end_mmm)

      BIND(SUBSTR(?production_timespan_begin, 1, 4) as ?begin_year)
      BIND(SUBSTR(?production_timespan_end, 1, 4) as ?end_year)
      BIND(STR(?begin_year) + " - " + STR(?end_year) as ?production_timespan_label)
    }
    OPTIONAL { ?production crm:P7_took_place_at ?production_place }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
          crm:P14.1_in_the_role_of bod-roles:artist ;
          crm:P02_has_range ?production_artist ;
          ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
          crm:P14.1_in_the_role_of bod-roles:author ;
          crm:P02_has_range ?production_author ;
          ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
          crm:P14.1_in_the_role_of bod-roles:binder ;
          crm:P02_has_range ?production_binder ;
          ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
          crm:P14.1_in_the_role_of bod-roles:commissioner ;
          crm:P02_has_range ?production_commissioner ;
          ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
          crm:P14.1_in_the_role_of bod-roles:dedicatee ;
          crm:P02_has_range ?production_dedicatee ;
          ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
          crm:P14.1_in_the_role_of bod-roles:formerOwner ;
          crm:P02_has_range ?production_formerOwner ;
          ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
          crm:P14.1_in_the_role_of bod-roles:patron ;
          crm:P02_has_range ?production_patron ;
          ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
          crm:P14.1_in_the_role_of bod-roles:scribe ;
          crm:P02_has_range ?production_scribe ;
          ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
          crm:P14.1_in_the_role_of bod-roles:signer ;
          crm:P02_has_range ?production_signer ;
          ]
    }
  }

  OPTIONAL {
    ?manifestation_singleton rdfs:label ?manifestation_singleton_preflabel .
    FILTER CONTAINS(?manifestation_singleton_preflabel, "Buchanan")
    BIND(REPLACE(STR(?manifestation_singleton_preflabel), "^M[sS].*Buchanan (.+)$", "$1") as ?shelfmark_buchanan)
    FILTER(?shelfmark_buchanan != STR(?manifestation_singleton_preflabel))
  }

  OPTIONAL {
    ?manifestation_singleton rdfs:label ?manifestation_singleton_preflabel .
    FILTER CONTAINS(?manifestation_singleton_preflabel, "Christ Church")
    BIND(REPLACE(STR(?manifestation_singleton_preflabel), "^Christ Church(?:(?: MS\\. )|(?:, ))(Allestree Library MS)?\\.?(.+)?$", "$1$2") as ?shelfmark_christ_church)
    FILTER(?shelfmark_christ_church != STR(?manifestation_singleton_preflabel))
  }

  OPTIONAL {
    ?manifestation_singleton rdfs:label ?manifestation_singleton_preflabel .
    FILTER CONTAINS(?manifestation_singleton_preflabel, "Barocci")
    BIND(REPLACE(STR(?manifestation_singleton_preflabel), "^MS. Barocci (.+)$", "$1") as ?shelfmark_barocci)
    FILTER(?shelfmark_barocci != STR(?manifestation_singleton_preflabel))
  }

  # MATERIAL
  OPTIONAL {
    ?manifestation_singleton crm:P45_consists_of ?material_uri .
    ?material_uri rdfs:label ?material_label .
  }

  # COLLECTIONS
  OPTIONAL {
    ?manifestation_singleton crm:P46i_forms_part_of ?collection .
  }


  BIND(REPLACE(REPLACE(STR(?expression), "^.+catalog\\/(.+)", "$1"), "\\/|:|#|%", "-") as ?expression_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/expression/bodley_", ?expression_local_id)) as ?expression_mmm)

  BIND(REPLACE(STR(?work), "^.*\\/(.+)", "$1") as ?work_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/work/bodley_", ?work_local_id)) as ?work_mmm)

  BIND(REPLACE(STR(?manifestation_singleton), "^.*\\/(.+)", "$1") as ?manifestation_singleton_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/manifestation_singleton/bodley_", ?manifestation_singleton_local_id)) as ?manifestation_singleton_mmm)

  BIND(REPLACE(STR(?child_manifestation_singleton), "^.*\\/(.+)", "$1") as ?child_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/manifestation_singleton/bodley_", ?child_local_id)) as ?child_manifestation_singleton_mmm)

  BIND(IRI(CONCAT("http://ldf.fi/mmm/event/bodley_production_", ?manifestation_singleton_local_id)) as ?production_mmm)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bodley_production_", ?manifestation_singleton_local_id)) as ?production_timespan_mmm)

  BIND(REPLACE(STR(?production_place), "^.*\\/(.+)", "$1") as ?production_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bodley_", REPLACE(?production_place_local_id, ":", "_"))) as ?production_place_mmm)

  BIND(REPLACE(STR(?prov_agent), "^.*\\/(.+)", "$1") as ?prov_agent_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?prov_agent_local_id)) as ?prov_agent_mmm)

  BIND(REPLACE(STR(?work_conception_author), "^.*\\/(.+)", "$1") as ?work_conception_author_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?work_conception_author_local_id)) as ?work_conception_author_mmm)

  BIND(REPLACE(STR(?production_artist), "^.*\\/(.+)", "$1") as ?production_artist_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?production_artist_local_id)) as ?production_artist_mmm)

  BIND(REPLACE(STR(?production_author), "^.*\\/(.+)", "$1") as ?production_author_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?production_author_local_id)) as ?production_author_mmm)

  BIND(REPLACE(STR(?production_binder), "^.*\\/(.+)", "$1") as ?production_binder_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?production_binder_local_id)) as ?production_binder_mmm)

  BIND(REPLACE(STR(?production_commissioner), "^.*\\/(.+)", "$1") as ?production_commissioner_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?production_commissioner_local_id)) as ?production_commissioner_mmm)

  BIND(REPLACE(STR(?production_dedicatee), "^.*\\/(.+)", "$1") as ?production_dedicatee_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?production_dedicatee_local_id)) as ?production_dedicatee_mmm)

  BIND(REPLACE(STR(?production_formerOwner), "^.*\\/(.+)", "$1") as ?production_formerOwner_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?production_formerOwner_local_id)) as ?production_formerOwner_mmm)

  BIND(REPLACE(STR(?production_patron), "^.*\\/(.+)", "$1") as ?production_patron_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?production_patron_local_id)) as ?production_patron_mmm)

  BIND(REPLACE(STR(?production_scribe), "^.*\\/(.+)", "$1") as ?production_scribe_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?production_scribe_local_id)) as ?production_scribe_mmm)

  BIND(REPLACE(STR(?production_signer), "^.*\\/(.+)", "$1") as ?production_signer_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bodley_", ?production_signer_local_id)) as ?production_signer_mmm)

  BIND(REPLACE(STR(?collection), "^.*\\/(.+)", "$1") as ?collection_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/collection/bodley_", ?collection_local_id)) as ?collection_mmm)

  BIND(IRI(CONCAT(STR(mmm:), "material/", LCASE(REPLACE(?material_uri, "[ \\\"\\~\\.\\-\\!\\$\\&\\'\\(\\)\\*\\+\\,\\;\\=\\/\\?\\@\\%\\_\\{\\}\\[\\]\\^\\s\\`]", "-")))) as ?material_uri_mmm)
}
