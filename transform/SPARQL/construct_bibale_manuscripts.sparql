PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>

CONSTRUCT {

  ?expression_mmm a frbroo:F2_Expression , crm:E33_Linguistic_Object;
    skos:prefLabel ?expression_label ;
    skos:altLabel ?expression_title ;
    mmm-schema:data_provider_url ?expression ;
    # crm:P72_has_language ?manifestation_work_language ;
    dct:source mmm-schema:Bibale .

  ?expression_creation_mmm a frbroo:F28_Expression_Creation ;
    frbroo:R17_created ?expression_mmm ;
    frbroo:R18_created ?manifestation_singleton_mmm ;
    frbroo:R19_created_a_realisation_of ?expression_work_mmm ;
    mmm-schema:carried_out_by_as_author ?expression_author_mmm ;
    mmm-schema:carried_out_by_as_scribe ?expression_scribe_mmm ;
    crm:P7_took_place_at ?expression_place_mmm ;
    # dct:source mmm-schema:Bibale .
    .

  ?production_mmm a crm:E12_Production ;
    crm:P108_has_produced ?manifestation_singleton_mmm ;
    crm:P4_has_time-span ?manuscript_timespan_mmm ;
    crm:P4_has_time-span ?production_timespan_mmm ;
    crm:P7_took_place_at ?production_place_mmm ;
    crm:P7_took_place_at ?manuscript_place_mmm ;
    mmm-schema:carried_out_by_as_author ?production_author_mmm ;
    mmm-schema:carried_out_by_as_binder ?production_binder_mmm ;
    mmm-schema:carried_out_by_as_commissioner ?production_commissioner_mmm ;
    mmm-schema:carried_out_by_as_creator ?production_creator_mmm ;
    mmm-schema:carried_out_by_as_illuminator ?production_illuminator_mmm ;
    mmm-schema:carried_out_by_as_printer ?production_printer_mmm ;
    mmm-schema:carried_out_by_as_scribe ?production_scribe_mmm ;
    dct:source mmm-schema:Bibale .

  ?manuscript_timespan_mmm a crm:E52_Time-Span ;
    skos:prefLabel ?manuscript_timespan_label ;
    crm:P82a_begin_of_the_begin ?manuscript_timespan_bb ;
    crm:P81a_end_of_the_begin ?manuscript_timespan_eb ;
    crm:P81b_begin_of_the_end ?manuscript_timespan_be ;
    crm:P82b_end_of_the_end ?manuscript_timespan_ee .

  ?production_timespan_mmm a crm:E52_Time-Span ;
    skos:prefLabel ?production_timespan_label ;
    crm:P82a_begin_of_the_begin ?production_timespan_bb ;
    crm:P81a_end_of_the_begin ?production_timespan_eb ;
    crm:P81b_begin_of_the_end ?production_timespan_be ;
    crm:P82b_end_of_the_end ?production_timespan_ee .

  ?manifestation_singleton_mmm a frbroo:F4_Manifestation_Singleton ;
    crm:P128_carries ?expression_mmm ;
    skos:prefLabel ?preflabel ;
    mmm-schema:data_provider_url ?manifestation_singleton ;
    mmm-schema:manuscript_work ?work_mmm ;  # shortcut
    mmm-schema:manuscript_author ?work_conception_author_mmm ;  # shortcut
    mmm-schema:manuscript_author ?expression_author_mmm ;  # shortcut
    mmm-schema:manuscript_author ?production_author_mmm ;  # shortcut
    crm:P46i_forms_part_of ?collection_mmm ;
    crm:P3_has_note ?note ;
    dct:source mmm-schema:Bibale
    .
}
WHERE
{
  ?manifestation_singleton a bibale:type:livre ;
                           rdfs:label ?preflabel .

  OPTIONAL { ?manifestation_singleton crm:P46i_forms_part_of ?collection }

  OPTIONAL { ?manifestation_singleton crm:P131_is_identified_by/rdfs:label ?note }

  # OPTIONAL {
  #   ?manifestation_singleton bibale:type:attested_or_curated/rdfs:label? "Volume conservé"
  # }
  # OPTIONAL {
  #   ?manifestation_singleton bibale:type:attested_or_curated/rdfs:label? "Volume ou texte attesté"
  # }

  # # TODO: These give the last known location of a manuscript
  # bibale:type:medium_country ?country ;
  # bibale:type:medium_repository ?rep ;
  # bibale:type:medium_settlement ?set ;
  # bibale:type:medium_shelfmark ?mark .
  #
  # ?country rdfs:label ?country_label .
  # ?rep rdfs:label ?rep_label .
  # ?set rdfs:label ?settlement_label .
  # ?mark rdfs:label ?shelfmark_label .

  # PRODUCTION DATE
  OPTIONAL {
    ?manifestation_singleton bibale:type:date/crm:P4_has_time-span ?manuscript_timespan .
    OPTIONAL { ?manuscript_timespan crm:P82a_begin_of_the_begin ?manuscript_timespan_bb }
    OPTIONAL { ?manuscript_timespan crm:P81a_end_of_the_begin ?manuscript_timespan_eb }
    OPTIONAL { ?manuscript_timespan crm:P81b_begin_of_the_end ?manuscript_timespan_be }
    OPTIONAL { ?manuscript_timespan crm:P82b_end_of_the_end ?manuscript_timespan_ee }
    OPTIONAL { ?manuscript_timespan rdfs:label ?manuscript_timespan_label }
  }

  # PRODUCTION PLACE
  OPTIONAL {
    ?manifestation_singleton crm:P53_has_former_or_current_location ?manuscript_place .
  }

  # PRODUCTION
  OPTIONAL {
    ?production crm:P108_has_produced ?manifestation_singleton ;
      a crm:E12_Production .
    OPTIONAL {
      ?production crm:P4_has_time-span ?production_timespan .
      OPTIONAL { ?production_timespan crm:P82a_begin_of_the_begin ?production_timespan_bb }
      OPTIONAL { ?production_timespan crm:P81a_end_of_the_begin ?production_timespan_eb }
      OPTIONAL { ?production_timespan crm:P81b_begin_of_the_end ?production_timespan_be }
      OPTIONAL { ?production_timespan crm:P82b_end_of_the_end ?production_timespan_ee }
      OPTIONAL { ?production_timespan rdfs:label ?production_timespan_label }
    }
    OPTIONAL {
      ?production crm:P7_took_place_at ?production_place .
    }
    OPTIONAL { ?production crm:P01i_is_domain_of [
         a crm:PC14_carried_out_by ;
         crm:P14.1_in_the_role_of [ crm:E55_Type  "Author" ] ;
         crm:P02_has_range ?production_author ;
     ] }
    OPTIONAL { ?production crm:P01i_is_domain_of [
         a crm:PC14_carried_out_by ;
         crm:P14.1_in_the_role_of [ crm:E55_Type  "Binder" ] ;
         crm:P02_has_range ?production_binder ;
     ] }
    OPTIONAL { ?production crm:P01i_is_domain_of [
         a crm:PC14_carried_out_by ;
         crm:P14.1_in_the_role_of [ crm:E55_Type  "Commissioner" ] ;
         crm:P02_has_range ?production_commissioner ;
     ] }
    OPTIONAL { ?production crm:P01i_is_domain_of [
         a crm:PC14_carried_out_by ;
         crm:P14.1_in_the_role_of [ crm:E55_Type  "Creator" ] ;
         crm:P02_has_range ?production_creator ;
     ] }
    OPTIONAL { ?production crm:P01i_is_domain_of [
         a crm:PC14_carried_out_by ;
         crm:P14.1_in_the_role_of [ crm:E55_Type  "Illuminator" ] ;
         crm:P02_has_range ?production_illuminator ;
     ] }
    OPTIONAL { ?production crm:P01i_is_domain_of [
         a crm:PC14_carried_out_by ;
         crm:P14.1_in_the_role_of [ crm:E55_Type  "Printer" ] ;
         crm:P02_has_range ?production_printer ;
     ] }  # Note, also other scribe path
    OPTIONAL { ?production crm:P01i_is_domain_of [
         a crm:PC14_carried_out_by ;
         crm:P14.1_in_the_role_of [ crm:E55_Type  "Scribe" ] ;
         crm:P02_has_range ?production_scribe ;
     ] }  # Note, also other scribe path
  }

  # EXPRESSION
  OPTIONAL {
    ?expression a frbroo2:F2_Expression .
    { ?expression crm:P128i_is_carried_by ?manifestation_singleton }
    UNION
    { ?expression ^crm:P128_carries ?manifestation_singleton }
    OPTIONAL { ?expression rdfs:label ?expression_label }
    OPTIONAL { ?expression crm:P102_has_title ?expression_title }
    OPTIONAL {
      ?expression_creation a frbroo2:F28_Expression_Creation ;
        frbroo2:R17_created ?expression ;
      OPTIONAL { ?expression_creation crm:P7_took_place_at ?expression_place }
      OPTIONAL {
        { ?expression_creation frbroo2:R19_created_a_realisation_of ?expression_work }
        UNION
        { ?expression_creation ^frbroo2:R19i_was_realised_through ?expression_work }
      }
      # OPTIONAL { frbroo2:R19_created_a_realisation_of ?manifestation_work }
      OPTIONAL { ?expression_creation crm:P01i_is_domain_of [
           a crm:PC14_carried_out_by ;
           crm:P14.1_in_the_role_of  [ crm:E55_Type  "Author" ] ;
           crm:P02_has_range ?expression_author ;
           ]
      }
      OPTIONAL { ?expression_creation crm:P01i_is_domain_of [
           a crm:PC14_carried_out_by ;
           crm:P14.1_in_the_role_of  [ crm:E55_Type  "Scribe" ] ;
           crm:P02_has_range ?expression_scribe ;
           ]
      }
    }
  }

  # WORK CONCEPTION AUTHOR
  OPTIONAL {
    { ?work crm:P128i_is_carried_by ?manifestation_singleton }
    UNION
    { ?work ^crm:P128_carries ?manifestation_singleton }
    ?work a frbroo2:F1_Work .
    OPTIONAL {
      ?work frbroo2:R16i_was_initiated_by ?work_conception .
      ?work_conception a frbroo2:F27_Work_Conception .
      OPTIONAL {
        ?work_conception crm:P01i_is_domain_of  [
          a crm:PC14_carried_out_by ;
          crm:P14.1_in_the_role_of [ crm:E55_Type  "Author" ] ;
          crm:P02_has_range ?work_conception_author ;
        ] ;
      }
    }
  }

  BIND(REPLACE(STR(?manifestation_singleton), "^.*\\/(.+)", "$1") as ?manifestation_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/manifestation_singleton/bibale_", ?manifestation_local_id)) as ?manifestation_singleton_mmm)

  BIND(REPLACE(STR(?collection), "^.*\\/(.+)", "$1") as ?collection_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/collection/bibale_", ?collection_local_id)) as ?collection_mmm)

  BIND(REPLACE(STR(?manuscript_timespan), "^.*\\/(.+)", "$1") as ?manuscript_timespan_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", REPLACE(?manuscript_timespan_local_id, ":", "_"))) as ?manuscript_timespan_mmm)
  BIND(REPLACE(STR(?manuscript_place), "^.*\\/(.+)", "$1") as ?manuscript_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?manuscript_place_local_id, ":", "_"))) as ?manuscript_place_mmm)

  BIND(IRI(CONCAT("http://ldf.fi/mmm/production/bibale_", ?manifestation_local_id, "_production")) as ?production_mmm)
  BIND(REPLACE(STR(?production_timespan), "^.*\\/(.+)", "$1") as ?production_timespan_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/time/bibale_", ?production_timespan_local_id)) as ?production_timespan_mmm)
  BIND(REPLACE(STR(?production_place), "^.*\\/(.+)", "$1") as ?production_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?production_place_local_id, ":", "_"))) as ?production_place_mmm)

  BIND(REPLACE(STR(?production_author), "^.*\\/(.+)", "$1") as ?production_author_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?production_author_local_id)) as ?production_author_mmm)
  BIND(REPLACE(STR(?production_binder), "^.*\\/(.+)", "$1") as ?production_binder_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?production_binder_local_id)) as ?production_binder_mmm)
  BIND(REPLACE(STR(?production_commissioner), "^.*\\/(.+)", "$1") as ?production_commissioner_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?production_commissioner_local_id)) as ?production_commissioner_mmm)
  BIND(REPLACE(STR(?production_creator), "^.*\\/(.+)", "$1") as ?production_creator_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?production_creator_local_id)) as ?production_creator_mmm)
  BIND(REPLACE(STR(?production_illuminator), "^.*\\/(.+)", "$1") as ?production_illuminator_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?production_illuminator_local_id)) as ?production_illuminator_mmm)
  BIND(REPLACE(STR(?production_printer), "^.*\\/(.+)", "$1") as ?production_printer_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?production_printer_local_id)) as ?production_printer_mmm)
  BIND(REPLACE(STR(?production_scribe), "^.*\\/(.+)", "$1") as ?production_scribe_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?production_scribe_local_id)) as ?production_scribe_mmm)

  BIND(REPLACE(STR(?expression), "^.*\\/(.+)", "$1") as ?expression_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/expression/bibale_", ?expression_local_id)) as ?expression_mmm)
  BIND(REPLACE(STR(?expression_creation), "^.*\\/(.+)", "$1") as ?expression_creation_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/expression_creation/bibale_", ?expression_creation_local_id)) as ?expression_creation_mmm)
  BIND(REPLACE(STR(?expression_place), "^.*\\/(.+)", "$1") as ?expression_place_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/place/bibale_", REPLACE(?expression_place_local_id, ":", "_"))) as ?expression_place_mmm)
  BIND(REPLACE(STR(?expression_work), "^.*\\/(.+)", "$1") as ?expression_work_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/work/bibale_", ?expression_work_local_id)) as ?expression_work_mmm)

  BIND(REPLACE(STR(?expression_author), "^.*\\/(.+)", "$1") as ?expression_author_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?expression_author_local_id)) as ?expression_author_mmm)
  BIND(REPLACE(STR(?expression_scribe), "^.*\\/(.+)", "$1") as ?expression_scribe_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?expression_scribe_local_id)) as ?expression_scribe_mmm)

  BIND(REPLACE(STR(?work), "^.*\\/(.+)", "$1") as ?work_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/work/bibale_", ?work_local_id)) as ?work_mmm)
  BIND(REPLACE(STR(?work_conception_author), "^.*\\/(.+)", "$1") as ?work_conception_author_local_id)
  BIND(IRI(CONCAT("http://ldf.fi/mmm/actor/bibale_", ?work_conception_author_local_id)) as ?work_conception_author_mmm)
}
