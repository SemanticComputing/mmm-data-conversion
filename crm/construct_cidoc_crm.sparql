PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

CONSTRUCT {
  ?work a frbroo:F1_Work  .

  ?expression a frbroo:F2_Expression ;
    crm:P72_has_language ?language .

  ?expression_creation a frbroo:F28_Expression_Creation ;
    frbroo:R17_created ?expression ;
    frbroo:R18_created ?manifestation_singleton ;
    frbroo:R19_created_a_realisation_of ?work ;
    mmm-schema:carried_out_by_as_author ?author ;
    crm:P7_took_place_at ?place ;
    crm:P4_has_time-span ?creation_timespan .

  ?creation_timespan a crm:E52_Time-Span ;
    skos:prefLabel ?creation_date_label ;
    skos:altLabel ?creation_date_observed ;
    crm:P79_beginning_is_qualified_by ?creation_date_start ;
    crm:P80_end_is_qualified_by ?creation_date_end .

  ?manifestation_singleton a frbroo:F4_Manifestation_Singleton ;
    crm:P128_carries ?expression ;
    rdfs:label ?title ;  # TODO: move to Expression, use Title class?
    skos:prefLabel ?preflabel ;
    crm:P1_is_identified_by ?manuscript ;
    crm:P43_has_dimension ?width_uri ;
    crm:P43_has_dimension ?height_uri ;
    crm:P43_has_dimension ?folios_uri ;
    crm:P43_has_dimension ?lines_uri ;
    crm:P43_has_dimension ?columns_uri ;
    crm:P43_has_dimension ?miniatures_uri ;
    crm:P45_consists_of ?material ;
    crm:P51_has_former_or_current_owner ?prov_agent ;
    mmm-schema:entry ?entry ;
    mmm-schema:manuscript_record ?manuscript ;
    mmm-schema:data_provider_url ?provider_url . # for showing the link in faceted search

  ?parent_manifestation_singleton crm:P46i_is_composed_of ?manifestation_singleton .

  ?width_uri a mmm-schema:Width ;
    crm:P90_has_value ?width_value ;
    crm:P91_has_unit "mm"@en .

  ?height_uri a mmm-schema:Height ;
    crm:P90_has_value ?height_value ;
    crm:P91_has_unit "mm"@en .

  ?folios_uri a mmm-schema:Folios ;
    crm:P90_has_value ?folios_value .

  ?lines_uri a mmm-schema:Lines ;
    crm:P90_has_value ?lines_value .

  ?columns_uri a mmm-schema:Columns ;
    crm:P90_has_value ?columns_value .

  ?miniatures_uri a mmm-schema:Miniatures ;
    crm:P90_has_value ?miniatures_value .

  ?acquisition_uri a ?event_type ;  # Acquisition or Observation
    skos:prefLabel ?cat_title ;
    ?event_property ?manifestation_singleton ;
    ?event_seller_or_holder_property ?seller ;
    crm:P22_transferred_title_to ?buyer ;
    mmm-schema:carried_out_by_as_selling_agent ?selling_agent ;
    mmm-schema:data_provider_url ?entry ;
    ?event_place_property ?source_agent_location ;
    # mmm-schema:place_literal ?cat_location_literal ;
    ?event_timespan_property ?cat_date ;
    crm:P179_had_sales_price ?monetary_amount_uri ;
    dc:source ?source .

  ?production a frbroo:E12_Production .

  ?monetary_amount_uri a crm:E97_Monetary_Amount ;
    crm:P180_has_currency ?sales_currency ;
    crm:P181_has_amount ?sales_price ;
    rdfs:comment ?sales_other_currency .

  # Monetary amount reification
  ?reification_monetary_amount_uri
    a rdf:Statement ;
    rdf:subject ?acquisition_uri ;
    rdf:predicate crm:P179_had_sales_price ;
    rdf:object ?monetary_amount_uri ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # provenance reification
  ?reification_owner_uri
    a rdf:Statement ;
    rdf:subject ?manifestation_singleton ;
    rdf:predicate crm:P51_has_former_or_current_owner ;
    rdf:object ?prov_agent ;
    mmm-schema:order ?prov_order ;
    rdfs:comment ?prov_comment ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # title reification
  ?reification_label_uri
    a rdf:Statement ;
    rdf:subject ?manifestation_singleton ;
    rdf:predicate rdfs:label ;
    rdf:object ?title ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # identifier reification
  ?reification_id_uri a rdf:Statement ;
    rdf:subject ?manifestation_singleton ;
    rdf:predicate crm:P48_has_preferred_identifier ;
    rdf:object ?manuscript_id ;
    dc:source ?source .

  # author reification
  ?reification_author_uri
    a rdf:Statement ;
    rdf:subject ?expression_creation ;
    rdf:predicate mmm-schema:carried_out_by_as_author ;
    rdf:object ?author ;
    mmm-schema:data_provider_url ?entry ;
    :observed_author ?observed_author ;
    dc:source ?source .

  # creation date reification
  ?reification_expression_creation_time_uri
    a rdf:Statement ;
    rdf:subject ?expression_creation ;
    rdf:predicate crm:P4_has_time-span ;
    rdf:object ?creation_timespan ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # artist reification

  # scribe reification

  # language reification
  ?reification_language_uri
    a rdf:Statement ;
    rdf:subject ?expression;
    rdf:predicate crm:P72_has_language ;
    rdf:object ?language ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # place reification
  ?reification_expression_creation_place_uri a rdf:Statement ;
    rdf:subject ?expression_creation ;
    rdf:predicate crm:P7_took_place_at ;
    rdf:object ?place ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # material reification
  ?reification_material_uri
    a rdf:Statement ;
    rdf:subject ?manifestation_singleton;
    rdf:predicate crm:P45_consists_of ;
    rdf:object ?material ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # use reification

  # folios reification
  ?reification_folios_uri
    a rdf:Statement;
    rdf:subject ?manifestation_singleton;
    rdf:predicate crm:P43_has_dimension ;
    rdf:object ?folios_uri ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # lines reification
  ?reification_lines_uri
    a rdf:Statement;
    rdf:subject ?manifestation_singleton;
    rdf:predicate crm:P43_has_dimension ;
    rdf:object ?lines_uri ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # columns reification
  ?reification_columns_uri
    a rdf:Statement;
    rdf:subject ?manifestation_singleton;
    rdf:predicate crm:P43_has_dimension ;
    rdf:object ?columns_uri ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # height reification
  ?reification_height_uri
    a rdf:Statement;
    rdf:subject ?manifestation_singleton;
    rdf:predicate crm:P43_has_dimension ;
    rdf:object ?height_uri ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # width reification
  ?reification_width_uri
    a rdf:Statement;
    rdf:subject ?manifestation_singleton;
    rdf:predicate crm:P43_has_dimension ;
    rdf:object ?width_uri ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # alternate size reification

  # miniatures reification
  ?reification_miniatures_uri
    a rdf:Statement;
    rdf:subject ?manifestation_singleton;
    rdf:predicate crm:P43_has_dimension ;
    rdf:object ?miniatures_uri ;
    mmm-schema:data_provider_url ?entry ;
    dc:source ?source .

  # historiated initials reification

  # decorated initials reification

  # binding reification

  # manuscript link reification

  # other Info reification

}
#SELECT *
WHERE {
  #BIND (<https://sdbm.library.upenn.edu/manuscripts/12463> AS ?manuscript)
  #BIND (<https://sdbm.library.upenn.edu/manuscripts/13354> AS ?manuscript) # manuscript with sales information: buyer, seller, sales agent known
  #    ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript ;
  #                      :entry_manuscripts_entry_id ?entry .
  #BIND (<https://sdbm.library.upenn.edu/entries/51493> AS ?entry) # entry with no manuscript record

  ?entry a :entries .
  FILTER(NOT EXISTS { ?entry :entries_deprecated true })

  OPTIONAL {
    ?entry_manuscript :entry_manuscripts_entry_id ?entry .
    ?entry_manuscript a :entry_manuscripts .
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "is" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript .
      ?manuscript a :manuscripts .
      ?manuscript :manuscripts_id ?manuscript_id_int .
    }
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "partial" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript_parent .
      ?entry_manuscript :entry_manuscripts_entry_id ?entry_partial_uri .
      ?manuscript_parent a :manuscripts .
      ?manuscript_parent :manuscripts_id ?manuscript_parent_id_int .
    }
    OPTIONAL {
      ?entry_manuscript :entry_manuscripts_relation_type "possible" .
      ?entry_manuscript :entry_manuscripts_manuscript_id ?manuscript .
      ?manuscript a :manuscripts .
      ?manuscript :manuscripts_id ?manuscript_id_int .
      BIND("Possible match of manuscript"@en as ?entry_comment)
      # TODO: Adjust model to fit ?entry_comment (add observation event or add to transaction information)
    }
  }
  # manuscripts_location is a deprecated field in SDBM
  # OPTIONAL { ?manuscript :manuscripts_location ?location . }

  ## TRANSACTION INFORMATION

  OPTIONAL {
    # ?entry :entries_transaction_type "sale" or "gift"
    ?sales_entry :sales_entry_id ?entry .
    ?sales_entry :sales_id ?sales_id .
    OPTIONAL { ?sales_entry :sales_price ?sales_price }
    OPTIONAL { ?sales_entry :sales_currency ?sales_currency }
    OPTIONAL { ?sales_entry :sales_other_currency ?sales_other_currency }
    OPTIONAL {
      ?sales_entry :sales_sold "Yes"
      BIND(crm:E8_Acquisition as ?event_type)
      BIND(crm:P24_transferred_title_of as ?event_property)
      BIND(crm:P7_took_place_at as ?event_place_property)
      BIND(crm:E52_has_time-span as ?event_timespan_property)
      BIND(crm:P23_transferred_title_from as ?event_seller_or_holder_property)
    }
    OPTIONAL {
      ?seller_agent :sale_agents_sale_id ?sales_entry .
      ?seller_agent :sale_agents_role "seller_or_holder" .
      ?seller_agent :sale_agents_agent_id/:names_id ?seller_id .
    }
    OPTIONAL {
      ?buyer_agent :sale_agents_sale_id ?sales_entry .
      ?buyer_agent :sale_agents_role "buyer" .
      ?buyer_agent :sale_agents_agent_id/:names_id ?buyer_id .
      BIND(crm:E8_Acquisition as ?event_type)
      BIND(crm:P24_transferred_title_of as ?event_property)
      BIND(crm:P7_took_place_at as ?event_place_property)
      BIND(crm:E52_has_time-span as ?event_timespan_property)
      BIND(crm:P23_transferred_title_from as ?event_seller_or_holder_property)
    }
    OPTIONAL {
      ?selling_agent_agent :sale_agents_sale_id ?sales_entry .
      ?selling_agent_agent :sale_agents_role "selling_agent" .
      ?selling_agent_agent :sale_agents_agent_id ?selling_agent_name .
      ?selling_agent_name :names_id ?selling_agent_id .
      # ?selling_agent_name (^:name_places_name_id)/:name_places_place_id/:places_id ?place_local_id .
      # BIND(IRI(CONCAT(STR(mmm:), "place/", STR(?place_local_id))) as ?selling_agent_location)
    }
  }
  OPTIONAL {
    BIND(mmm-schema:Manuscript_Observation as ?event_type)
    BIND(mmm-schema:observed_manuscript as ?event_property)
    BIND(mmm-schema:observed_location as ?event_place_property)
    BIND(mmm-schema:observed_time-span as ?event_timespan_property)
    BIND(mmm-schema:observed_owner as ?event_seller_or_holder_property)
  }

  ## MANUSCRIPT DETAILS ##

  # TODO: Add dates

  OPTIONAL {
    ?provenance_entry :provenance_entry_id ?entry ;
                      :provenance_provenance_agent_id/:names_id ?prov_agent_id ;
                                                     :provenance_order ?prov_order .
    OPTIONAL { ?provenance_entry :provenance_comment ?prov_comment . }
  }

  # title
  ?entry_title :entry_titles_entry_id ?entry .
  ?entry_title :entry_titles_id ?title_id .
  OPTIONAL {
    ?entry_title :entry_titles_title|:entry_titles_common_title ?title .
    FILTER(STR(?title) != "")
  }
  OPTIONAL {
    ?entry_title :entry_titles_common_title ?common_title .
    FILTER(STR(?common_title) != "")
  }

  # author
  OPTIONAL {
    ?entry_author :entry_authors_entry_id ?entry .
    ?entry_author :entry_authors_author_id/:names_id ?author_id .
    ?entry_author :entry_authors_observed_name ?observed_author .
  }

  # creation date
  OPTIONAL {
    ?entry_date :entry_dates_entry_id ?entry .
    ?entry_date :entry_dates_id ?creation_date_id .
    ?entry_date :entry_dates_date_normalized_start ?creation_date_start .
    ?entry_date :entry_dates_date_normalized_end ?creation_date_end .
    ?entry_date :entry_dates_observed_date ?creation_date_observed .
  }

  # artist

  # scribe

  # language
  OPTIONAL {
    ?entry_language :entry_languages_entry_id ?entry .
    ?entry_language :entry_languages_language_id/:languages_id ?language_id .
    ?entry_language :entry_languages_language_id/:languages_name ?language_ .
    BIND(STRLANG(?language_, 'en') AS ?language)
  }

  # place
  OPTIONAL {
    ?entry_place :entry_places_entry_id ?entry .
    ?entry_place :entry_places_place_id/:places_id ?place_id .
  }

  # material
  OPTIONAL {
    ?entry_material :entry_materials_entry_id ?entry .
    ?entry_material :entry_materials_id ?material_id .
    ?entry_material :entry_materials_material ?material_ .
    BIND(STRLANG(?material_, 'en') AS ?material)
  }

  # use

  # folios
  OPTIONAL {
    ?entry :entries_folios ?folios_value .
  }

  # lines
  OPTIONAL {
    ?entry :entries_num_lines ?lines_value .
  }

  # columns
  OPTIONAL {
    ?entry :entries_num_columns ?columns_value .
  }

  # width
  OPTIONAL {
    ?entry :entries_width ?width_value .
  }

  # height
  OPTIONAL {
    ?entry :entries_height ?height_value .
  }

  # alternate size

  # full-page miniatures
  OPTIONAL {
    ?entry :entries_miniatures_fullpage ?miniatures_fullpage_value .
  }

  # large miniatures
  OPTIONAL {
    ?entry :entries_miniatures_large ?miniatures_large_value .
  }

  # small miniatures
  OPTIONAL {
    ?entry :entries_miniatures_small ?miniatures_small_value .
  }

  # unspec. sized miniatures
  OPTIONAL {
    ?entry :entries_miniatures_unspec_size ?miniatures_unspec_value .
  }


  # historiated initials

  # decorated initials

  # binding

  # manuscript link

  # other Info

  ## /MANUSCRIPT DETAILS ##

  ## SOURCE ##
  OPTIONAL {
    ?entry :entries_source_id ?entry_source .
    OPTIONAL{ ?entry_source :sources_id ?source_id }
    OPTIONAL{
      ?entry_source :sources_title ?cat_title
      FILTER(STR(?cat_title) != "")
    }
    OPTIONAL{
      ?entry_source :sources_date ?cat_date_raw
      FILTER(STR(?cat_date_raw) != "")
    }
    OPTIONAL{ ?entry_source :sources_link ?cat_link }
    OPTIONAL{
      ?source_agent :source_agents_source_id ?entry_source .
      ?source_agent :source_agents_agent_id ?source_agent_name .
      ?source_agent_name (^:name_places_name_id)/:name_places_place_id/:places_id ?source_place_local_id .
      BIND(IRI(CONCAT(STR(mmm:), "place/", STR(?source_place_local_id))) as ?source_agent_location)
    }
  }

  BIND(SUBSTR(?cat_date_raw, 1, 4) as ?cat_date_year)
  BIND(SUBSTR(?cat_date_raw, 5, 2) as ?cat_date_month)
  BIND(SUBSTR(?cat_date_raw, 7) as ?cat_date_day)
  BIND(?cat_date_year + "-" + ?cat_date_month + "-" + ?cat_date_day as ?cat_date)

  BIND(REPLACE(STR(?entry), "^.*\\/(.+)", "$1") as ?entry_id)
  BIND(REPLACE(STR(?entry_partial_uri), "^.*\\/(.+)", "$1") as ?entry_partial_id)

  # generate IRIs in LDF.fi namespace
  BIND(STR(?manuscript_id_int) AS ?manuscript_id_)
  BIND(COALESCE(?manuscript_id_, "part_" + STR(?entry_partial_id), "orphan_" + STR(?entry_id)) AS ?manuscript_id)
  BIND(IRI(CONCAT(STR(mmm:), "work/", ?manuscript_id)) as ?work)
  BIND(IRI(CONCAT(STR(mmm:), "expression/", ?manuscript_id)) as ?expression)
  BIND(IRI(CONCAT(STR(mmm:), "expression_creation/", ?manuscript_id)) as ?expression_creation)
  BIND(IRI(CONCAT(STR(mmm:), "manifestation_singleton/", ?manuscript_id)) as ?manifestation_singleton)
  BIND(IRI(CONCAT(STR(mmm:), "manifestation_singleton/", STR(?manuscript_parent_id_int))) as ?parent_manifestation_singleton)
  BIND(IRI(CONCAT(STR(mmm:), "production/", ?manuscript_id)) as ?production)
  BIND(IRI(CONCAT(STR(mmm:), "source/", STR(?source_id))) as ?source)
  BIND(IRI(CONCAT(STR(mmm:), "actors/", STR(?author_id))) as ?author)
  BIND(IRI(CONCAT(STR(mmm:), "actors/", STR(?seller_id))) as ?seller)
  BIND(IRI(CONCAT(STR(mmm:), "actors/", STR(?buyer_id))) as ?buyer)
  BIND(IRI(CONCAT(STR(mmm:), "actors/", STR(?selling_agent_id))) as ?selling_agent)
  BIND(IRI(CONCAT(STR(mmm:), "actors/", STR(?prov_agent_id))) as ?prov_agent)
  BIND(IRI(CONCAT(STR(mmm:), "place/", STR(?place_id))) as ?place)
  BIND(IRI(CONCAT(STR(mmm:), "timespan/", STR(?creation_date_id))) as ?creation_timespan)
  BIND(IRI(CONCAT(STR(mmm:), "observation/", ?entry_id)) as ?acquisition_uri)
  BIND(IRI(CONCAT(STR(mmm:), "height/", ?entry_id)) as ?height_uri)
  BIND(IRI(CONCAT(STR(mmm:), "width/", ?entry_id)) as ?width_uri)
  BIND(IRI(CONCAT(STR(mmm:), "folios/", ?entry_id)) as ?folios_uri)
  BIND(IRI(CONCAT(STR(mmm:), "lines/", ?entry_id)) as ?lines_uri)
  BIND(IRI(CONCAT(STR(mmm:), "columns/", ?entry_id)) as ?columns_uri)
  BIND(IRI(CONCAT(STR(mmm:), "miniatures/", ?entry_id)) as ?miniatures_uri)
  BIND(IRI(CONCAT(STR(mmm:), "monetary_amounts/", ?sales_id)) as ?monetary_amount_uri)

  BIND(?miniatures_large_value + ?miniatures_small_value + ?miniatures_fullpage_value + ?miniatures_unspec_value as ?miniatures_value)
  BIND(CONCAT(STR(?creation_date_start),"-",STR(?creation_date_end)) as ?creation_date_label)
  BIND(COALESCE(?common_title, ?title, ?manuscript_id) as ?preflabel)  # TODO: Get most common value from ?title
  # TODO: for partial relations, take title as prefLabel

  BIND(COALESCE(?manuscript, ?entry) as ?provider_url)

  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_author_", STR(?author_id))) as ?reification_author_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_owner_", STR(?prov_agent_id))) as ?reification_owner_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_expression_creation_place_", STR(?place_id))) as ?reification_expression_creation_place_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_expression_creation_time_", STR(?creation_date_id))) as ?reification_expression_creation_time_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_identifier_", ?manuscript_id)) as ?reification_id_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_material_", ?material_id)) as ?reification_material_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_folios_", STR(?folios_value))) as ?reification_folios_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_lines_", STR(?lines_value))) as ?reification_lines_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_columns_", STR(?columns_value))) as ?reification_columns_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_height_", STR(?height_value))) as ?reification_height_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_width_", STR(?width_value))) as ?reification_width_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_miniatures_", STR(?miniatures_value))) as ?reification_miniatures_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_language_", REPLACE(?language, " ", "-"))) as ?reification_language_uri)
  BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_label_", STR(?title_id))) as ?reification_label_uri)
  # BIND(IRI(CONCAT(STR(mmm:), "reification/", ?manuscript_id, "_label_", STR(?title_id), "_", SUBSTR(REPLACE(?title, "[\\\"\\~\\.\\-\\!\\$\\&\\'\\(\\)\\*\\+\\,\\;\\=\\/\\?\\#\\@\\%\\_\\{\\}\\[\\]\\^\\s\\`]", "-"), 0, 21))) as ?reification_label_uri)
}
