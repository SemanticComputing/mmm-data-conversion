PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX frbroo2: <http://www.cidoc-crm.org/frbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bibale: <http://bibale.irht.cnrs.fr/>
# TODO:
# - mint MMM URIs for resources

CONSTRUCT {
  ?work a frbroo:F1_Work  ;
    skos:prefLabel ?work_pref_label ;
    skos:altLabel ?work_alt_label ;
    mmm-schema:data_provider_url ?work ;
    dct:source mmm-schema:Bibale .

  ?work_conception a frbroo:F27_Work_Conception ;
    skos:prefLabel ?work_conception_pref_label ;
    frbroo:R16_initiated ?work ;
    mmm-schema:carried_out_by_as_author ?work_conception_author ;
    mmm-schema:carried_out_by_as_translator ?work_conception_translator ;
    mmm-schema:carried_out_by_as_editor ?work_conception_editor ;
    mmm-schema:carried_out_by_as_commissioner ?work_conception_commissioner ;
    mmm-schema:carried_out_by_as_recipient ?work_conception_recipient ;
    dct:source mmm-schema:Bibale .

  # ?expression a frbroo:F2_Expression , crm:E33_Linguistic_Object;
  #   skos:altLabel ?work_title ;
  #   crm:P72_has_language ?work_language ;
  #   dct:source mmm-schema:Bodley .
  #
  ?expression a frbroo:F2_Expression;
    skos:prefLabel ?expression_label ;
    skos:altLabel ?expression_title ;
    mmm-schema:data_provider_url ?expression ;
    dct:source mmm-schema:Bibale .

  ?expression_creation a frbroo:F28_Expression_Creation ;
    frbroo:R17_created ?expression ;
    frbroo:R18_created ?manifestation_singleton ;
    frbroo:R19_created_a_realisation_of ?work ;
    mmm-schema:carried_out_by_as_author ?expression_author ;
    dct:source mmm-schema:Bibale .
  #   # crm:P7_took_place_at ?place ;
  #   # crm:P4_has_time-span ?creation_timespan ;
  #   # ?creation_timespan a crm:E52_Time-Span ;
  #   #   skos:prefLabel ?creation_date_label ;
  #   #   skos:altLabel ?creation_date_observed ;
  #   #   crm:P79_beginning_is_qualified_by ?creation_date_start ;
  #   #   crm:P80_end_is_qualified_by ?creation_date_end .
  #

  ?production a crm:E12_Production ;
    crm:P108_has_produced ?manifestation_singleton ;
    crm:P4_has_time-span ?production_timespan ;
    crm:P7_took_place_at ?production_place ;
    mmm-schema:carried_out_by_as_scribe ?production_scribe ;
    dct:source mmm-schema:Bibale .

  ?production_timespan a crm:E52_Time-Span ;
    skos:prefLabel ?production_timespan_label ;
    crm:P82a_begin_of_the_begin ?production_timespan_bb ;
    crm:P81a_end_of_the_begin ?production_timespan_eb ;
    crm:P81b_begin_of_the_end ?production_timespan_be ;
    crm:P82b_end_of_the_end ?production_timespan_ee .

  ?manifestation_singleton a frbroo:F4_Manifestation_Singleton ;
    # crm:P128_carries ?expression ;
    skos:prefLabel ?preflabel ;
    mmm-schema:data_provider_url ?manifestation_singleton ;
    dct:source mmm-schema:Bibale .
}
WHERE
{
  ?manifestation_singleton a bibale:type:livre ;
                           rdfs:label ?preflabel .

  # PRODUCTION
  OPTIONAL {
    ?production crm:P108_has_produced ?manifestation_singleton ;
      a crm:E12_Production .
    OPTIONAL {
      ?production crm:P4_has_time-span ?production_timespan .
      OPTIONAL { ?production_timespan crm:P82a_begin_of_the_begin ?production_timespan_bb }
      OPTIONAL { ?production_timespan crm:P81a_end_of_the_begin ?production_timespan_eb }
      OPTIONAL { ?production_timespan crm:P81b_begin_of_the_end ?production_timespan_be }
      OPTIONAL { ?production_timespan crm:P82b_end_of_the_end ?production_timespan_ee }
      OPTIONAL { ?production_timespan rdfs:label ?production_timespan_label }
    }
    OPTIONAL {
      ?production crm:P7_took_place_at ?production_place .
    }
    OPTIONAL { ?production crm:P01i_is_domain_of [
         a crm:PC14_carried_out_by ;
         crm:P14.1_in_the_role_of [ crm:E55_Type  "Scribe" ] ;
         crm:P02_has_range ?production_scribe ;
     ] }  # Note, also other scribe path
  }

  # EXPRESSION
  OPTIONAL {
    ?expression crm:P128i_is_carried_by ?manifestation_singleton .
    OPTIONAL { ?expression rdfs:label ?expression_label }
    OPTIONAL { ?expression crm:P102_has_title ?expression_title }
    OPTIONAL {
      ?expression_creation a frbroo2:F28_Expression_Creation ;
        frbroo2:R17_created ?expression .
      # OPTIONAL { frbroo2:R19_created_a_realisation_of ?work }
      OPTIONAL { ?expression_creation crm:P01i_is_domain_of [
           a crm:PC14_carried_out_by ;
           crm:P14.1_in_the_role_of  [ crm:E55_Type  "Author" ] ;
           crm:P02_has_range ?expression_author ;
           ]
      }
    }
  }

  # WORK
  OPTIONAL {
    ?work a frbroo2:F1_Work .
    { ?work crm:P128i_is_carried_by ?manifestation_singleton }
    UNION
    { ?work ^crm:P128_carries ?manifestation_singleton }
    OPTIONAL { ?work rdfs:label ?work_pref_label }
    OPTIONAL { ?work crm:P102_has_title/rdfs:label ?work_alt_label }
    OPTIONAL {
      ?work frbroo2:R16i_was_initiated_by ?work_conception .
      OPTIONAL {
        ?work_conception crm:P01i_is_domain_of  [
          a crm:PC14_carried_out_by ;
          crm:P14.1_in_the_role_of [ crm:E55_Type  "Author" ] ;
          crm:P02_has_range ?work_conception_author ;
        ] ;
      }
      OPTIONAL {
        ?work_conception crm:P01i_is_domain_of  [
          a crm:PC14_carried_out_by ;
          crm:P14.1_in_the_role_of [ crm:E55_Type  "Translator" ] ;
          crm:P02_has_range ?work_conception_translator ;
        ] ;
      }
      OPTIONAL {
        ?work_conception crm:P01i_is_domain_of  [
          a crm:PC14_carried_out_by ;
          crm:P14.1_in_the_role_of [ crm:E55_Type  "Editor" ] ;
          crm:P02_has_range ?work_conception_editor ;
        ] ;
      }
      OPTIONAL {
        ?work_conception crm:P01i_is_domain_of  [
          a crm:PC14_carried_out_by ;
          crm:P14.1_in_the_role_of [ crm:E55_Type  "Commissioner" ] ;
          crm:P02_has_range ?work_conception_commissioner ;
        ] ;
      }
      OPTIONAL {
        ?work_conception crm:P01i_is_domain_of  [
          a crm:PC14_carried_out_by ;
          crm:P14.1_in_the_role_of [ crm:E55_Type  "Recipient" ] ;
          crm:P02_has_range ?work_conception_recipient ;
        ] ;
      }
    }
  }

}
