PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

# TODO: Can organizations be separated?

CONSTRUCT {
  ?person a mmm-schema:Person ;
    skos:prefLabel ?name ;
    mmm-schema:viaf ?viaf_id ;
    mmm-schema:person_place ?mmm_place ;  # Place/nationality
} WHERE {
  ?name_id a :names ;
           :names_name ?name ;
           :names_id ?person_local_id .
  OPTIONAL { ?name_id :names_viaf_id ?viaf_id . }
  BIND(IRI(CONCAT(STR(mmm:), "person/", STR(?person_local_id))) as ?person)

  OPTIONAL {
    ?name_place_id :name_places_name_id ?name_id ;
                   :name_places_place_id ?place_id ;
                   a :name_places .
    ?place_id :places_id ?place_local_id .
    BIND(IRI(CONCAT(STR(mmm:), "place/", STR(?place_local_id))) as ?mmm_place)
  }
}
