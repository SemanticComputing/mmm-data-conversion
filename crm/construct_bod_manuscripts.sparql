PREFIX : <https://sdbm.library.upenn.edu/>
PREFIX frbr: <http://www.cidoc-crm.org/frbr/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#>
PREFIX mmm: <http://ldf.fi/mmm/>
PREFIX mmm-schema: <http://ldf.fi/mmm/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bod-roles: <https://medieval.bodleian.ox.ac.uk/catalog/authority/roles/>

# TODO:
# - mint MMM URIs for resources

CONSTRUCT {
  ?work a frbroo:F1_Work  ;
    skos:prefLabel ?work_preflabel ;
    dct:source mmm-schema:Bodley .

  ?work_conception a frbroo:F27_Work_Conception ;
    skos:prefLabel ?work_conception_pref_label ;
    frbroo:R16_initiated ?work ;
    mmm-schema:carried_out_by_as_author ?work_conception_author ;
    dct:source mmm-schema:Bodley .

  ?expression a frbroo:F2_Expression , crm:E33_Linguistic_Object;
    skos:altLabel ?work_title ;
    skos:prefLabel ?expression_label ;
    crm:P72_has_language ?work_language ;
    dct:source mmm-schema:Bodley .

  ?production a crm:E12_Production ;
    crm:P108_has_produced ?manifestation_singleton ;
    crm:P4_has_time-span ?production_timespan ;
    crm:P7_took_place_at ?production_place ;
    mmm-schema:carried_out_by_as_artist ?production_artist ;
    mmm-schema:carried_out_by_as_author ?production_author ;
    mmm-schema:carried_out_by_as_binder ?production_binder ;
    mmm-schema:carried_out_by_as_commissioner ?production_commissioner ;
    mmm-schema:carried_out_by_as_dedicatee ?production_dedicatee ;
    mmm-schema:carried_out_by_as_formerOwner ?production_formerOwner ;
    mmm-schema:carried_out_by_as_patron ?production_patron ;
    mmm-schema:carried_out_by_as_scribe ?production_scribe ;
    mmm-schema:carried_out_by_as_signer ?production_signer ;
    dct:source mmm-schema:Bodley .

  ?production_timespan a crm:E52_Time-Span ;
    skos:prefLabel ?production_timespan_label ;
    crm:P79_beginning_is_qualified_by ?production_timespan_begin ;
    crm:P80_end_is_qualified_by ?production_timespan_end .

  ?manifestation_singleton a frbroo:F4_Manifestation_Singleton ;
    # crm:P128_carries ?expression ;
    skos:prefLabel ?preflabel ;
    # crm:P43_has_dimension ?width_uri ;
    # crm:P43_has_dimension ?height_uri ;
    # crm:P43_has_dimension ?folios_uri ;
    # crm:P43_has_dimension ?lines_uri ;
    # crm:P43_has_dimension ?columns_uri ;
    # crm:P43_has_dimension ?miniatures_uri ;
    # crm:P45_consists_of ?material ;
    crm:P51_has_former_or_current_owner ?prov_agent ;
    # mmm-schema:entry ?entry ;
    # mmm-schema:manuscript_record ?manuscript ;
    mmm-schema:data_provider_url ?manifestation_singleton ;
    dct:source mmm-schema:Bodley .

}
WHERE
{
  ?manifestation_singleton a frbr:F4_Manifestation_Singleton ;
    rdfs:label ?preflabel .
  OPTIONAL { ?manifestation_singleton crm:P51_has_former_or_current_owner ?prov_agent }

  # EXPRESSION / WORK
  OPTIONAL {
    ?manifestation_singleton frbr:R42_is_representative_manifestation_singleton_for ?expression .
    ?expression rdfs:label ?expression_label .
    OPTIONAL {
      ?expression frbr:R3i_realises ?work .
      ?work rdfs:label ?work_title .
      OPTIONAL { ?work crm:P102_has_title ?work_preflabel . }
      OPTIONAL { ?work crm:P72_has_language ?work_language . }
      OPTIONAL {
        ?work frbr:R16i_was_initiated_by ?work_conception .
        ?work_conception rdfs:label ?work_conception_pref_label .
        ?work_conception crm:P14_carried_out_by ?work_conception_author .
      }
    }
  }

  # PRODUCTION
  OPTIONAL {
    ?manifestation_singleton crm:P108i_was_produced_by ?production .
    ?production rdfs:label ?production_label .
    OPTIONAL {
      ?production crm:P4_has_time-span ?production_timespan .
      ?production_timespan crm:P82a_begin_of_the_begin ?production_timespan_begin .
      ?production_timespan crm:P82b_end_of_the_end ?production_timespan_end .
    }
    OPTIONAL { ?production crm:P7_took_place_at ?production_place }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
        crm:P14.1_in_the_role_of bod-roles:artist ;
        crm:P02_has_range ?production_artist ;
      ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
        crm:P14.1_in_the_role_of bod-roles:author ;
        crm:P02_has_range ?production_author ;
      ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
        crm:P14.1_in_the_role_of bod-roles:binder ;
        crm:P02_has_range ?production_binder ;
      ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
        crm:P14.1_in_the_role_of bod-roles:commissioner ;
        crm:P02_has_range ?production_commissioner ;
      ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
        crm:P14.1_in_the_role_of bod-roles:dedicatee ;
        crm:P02_has_range ?production_dedicatee ;
      ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
        crm:P14.1_in_the_role_of bod-roles:formerOwner ;
        crm:P02_has_range ?production_formerOwner ;
      ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
        crm:P14.1_in_the_role_of bod-roles:patron ;
        crm:P02_has_range ?production_patron ;
      ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
        crm:P14.1_in_the_role_of bod-roles:scribe ;
        crm:P02_has_range ?production_scribe ;
      ]
    }
    OPTIONAL {
      ?production crm:P01i_is_domain_of [
        crm:P14.1_in_the_role_of bod-roles:signer ;
        crm:P02_has_range ?production_signer ;
      ]
    }
  }

  BIND(SUBSTR(?production_timespan_begin, 1, 4) as ?begin_year)
  BIND(SUBSTR(?production_timespan_end, 1, 4) as ?end_year)
  BIND(STR(?begin_year) + "-" + STR(?end_year) as ?production_timespan_label)
}
